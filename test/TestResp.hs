{-# LANGUAGE OverloadedStrings #-}

module TestResp where

import Data.ByteString qualified as BS
import Test.Tasty
import Test.Tasty.HUnit
import Text.Parsec

import Resp

-- helper
parseOnly :: Parsec BS.ByteString () a -> BS.ByteString -> Either ParseError a
parseOnly p = runParser p () ""

tests :: TestTree
tests =
    testGroup
        "RESP Parser Tests"
        [ testGroup
            "resp parser"
            [ testCase "simple string +OK" $
                parseOnly resp "+OK\r\n" @?= Right (Str "OK")
            , testCase "empty simple string" $
                parseOnly resp "+\r\n" @?= Right (Str "")
            , testCase "error -ERR foo" $
                parseOnly resp "-ERR foo\r\n" @?= Right (StrErr "ERR foo")
            , testCase "positive integer" $
                parseOnly resp ":42\r\n" @?= Right (Int 42)
            , testCase "negative integer" $
                parseOnly resp ":-7\r\n" @?= Right (Int (-7))
            , testCase "bulk string 'foo'" $
                parseOnly resp "$3\r\nfoo\r\n" @?= Right (BulkStr "foo")
            , testCase "empty bulk string" $
                parseOnly resp "$0\r\n\r\n" @?= Right (BulkStr "")
            , testCase "null bulk string" $
                parseOnly resp "$-1\r\n" @?= Right NullBulk
            , testCase "array of ints" $
                parseOnly resp "*2\r\n:1\r\n:2\r\n" @?= Right (Array 2 [Int 1, Int 2])
            , testCase "nested array" $
                parseOnly resp "*2\r\n*1\r\n+OK\r\n:5\r\n"
                    @?= Right
                        (Array 2 [Array 1 [Str "OK"], Int 5])
            ]
        , testGroup
            "resps parser"
            [ testCase "multiple RESP values" $
                parseOnly resps "+OK\r\n:1\r\n" @?= Right [Str "OK", Int 1]
            , testCase "zero RESP values" $
                parseOnly resps "" @?= Right []
            ]
        , testGroup
            "respsWithRemainder parser"
            [ testCase "remainder after multiple RESP values" $
                parseOnly respsWithRemainder "+OK\r\n:1\r\nXYZ" @?= Right ([Str "OK", Int 1], "XYZ")
            , testCase "all input consumed, empty remainder" $
                parseOnly respsWithRemainder "+OK\r\n:1\r\n" @?= Right ([Str "OK", Int 1], "")
            , testCase "single RESP with remainder" $
                parseOnly respsWithRemainder "$3\r\nfoo\r\nBAR" @?= Right ([BulkStr "foo"], "BAR")
            ]
        ]
