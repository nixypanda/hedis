{-# LANGUAGE OverloadedStrings #-}

module TestResp.TestCore (tests) where

import Hedgehog
import Hedgehog.Gen qualified as Gen
import Hedgehog.Range qualified as Range
import Test.Tasty
import Test.Tasty.HUnit
import Test.Tasty.Hedgehog (testProperty)

import Data.ByteString (ByteString)
import Data.ByteString qualified as BS
import Data.String (fromString)
import Text.Parsec

import Resp.Core

-- helper
parseOnly :: Parsec BS.ByteString () a -> BS.ByteString -> Either ParseError a
parseOnly p = runParser p () ""

tests :: TestTree
tests =
    testGroup
        "RESP Parser Tests"
        [ testGroup
            "respParser parser"
            [ testCase "simple string +OK" $
                parseOnly respParser "+OK\r\n" @?= Right (Str "OK")
            , testCase "empty simple string" $
                parseOnly respParser "+\r\n" @?= Right (Str "")
            , testCase "error -ERR foo" $
                parseOnly respParser "-ERR foo\r\n" @?= Right (StrErr "ERR foo")
            , testCase "positive integer" $
                parseOnly respParser ":42\r\n" @?= Right (Int 42)
            , testCase "negative integer" $
                parseOnly respParser ":-7\r\n" @?= Right (Int (-7))
            , testCase "bulk string 'foo'" $
                parseOnly respParser "$3\r\nfoo\r\n" @?= Right (BulkStr "foo")
            , testCase "empty bulk string" $
                parseOnly respParser "$0\r\n\r\n" @?= Right (BulkStr "")
            , testCase "null bulk string" $
                parseOnly respParser "$-1\r\n" @?= Right NullBulk
            , testCase "array of ints" $
                parseOnly respParser "*2\r\n:1\r\n:2\r\n" @?= Right (Array 2 [Int 1, Int 2])
            , testCase "nested array" $
                parseOnly respParser "*2\r\n*1\r\n+OK\r\n:5\r\n"
                    @?= Right
                        (Array 2 [Array 1 [Str "OK"], Int 5])
            ]
        , testGroup
            "resps parser"
            [ testCase "multiple RESP values" $
                parseOnly resps "+OK\r\n:1\r\n" @?= Right [Str "OK", Int 1]
            , testCase "zero RESP values" $
                parseOnly resps "" @?= Right []
            ]
        , testGroup
            "respsWithRemainder parser"
            [ testCase "remainder after multiple RESP values" $
                parseOnly respsWithRemainder "+OK\r\n:1\r\nXYZ" @?= Right ([Str "OK", Int 1], "XYZ")
            , testCase "all input consumed, empty remainder" $
                parseOnly respsWithRemainder "+OK\r\n:1\r\n" @?= Right ([Str "OK", Int 1], "")
            , testCase "single RESP with remainder" $
                parseOnly respsWithRemainder "$3\r\nfoo\r\nBAR" @?= Right ([BulkStr "foo"], "BAR")
            ]
        , testProperty "encode/decode dual" prop_EncodeDecodeDual
        ]

genByteString :: Gen ByteString
genByteString =
    fromString <$> Gen.list (Range.linear 0 20) (Gen.element ['a' .. 'z'])

genResp :: Int -> Gen Resp
genResp 0 =
    Gen.choice
        [ Str <$> genByteString
        , StrErr <$> genByteString
        , BulkStr <$> genByteString
        , Int <$> Gen.int (Range.linear (-100000) 100000)
        , pure NullBulk
        , pure NullArray
        ]
genResp n =
    Gen.recursive
        Gen.choice
        [ Str <$> genByteString
        , StrErr <$> genByteString
        , BulkStr <$> genByteString
        , Int <$> Gen.int (Range.linear (-100000) 100000)
        , pure NullBulk
        , pure NullArray
        ]
        [ do
            xs <- Gen.list (Range.linear 0 5) (genResp (n - 1))
            pure $ Array (length xs) xs
        ]

prop_EncodeDecodeDual :: Property
prop_EncodeDecodeDual = property $ do
    resp <- forAll (genResp 4)

    let bs = encode resp
    decoded <- evalEither (decode bs)

    decoded === resp
