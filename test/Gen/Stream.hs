module Gen.Stream (
    genConcreteStreamId,
    genXAddStreamId,
    genXRange,
    genXReadStreamId,
    genInsertSeq,
) where

import Hedgehog (Gen)
import Hedgehog.Gen qualified as Gen
import Hedgehog.Range qualified as Range

import Store.Backend.StreamMap

genInsertSeq :: Gen [XAddStreamId]
genInsertSeq = Gen.list (Range.linear 100 200) genStreamId

genStreamId :: Gen XAddStreamId
genStreamId = ExplicitId <$> Gen.int (Range.linear 1 1_000_000) <*> genIdSeq

genIdSeq :: Gen IdSeq
genIdSeq = Seq <$> Gen.int (Range.linear 0 10)

genConcreteStreamId :: Gen ConcreteStreamId
genConcreteStreamId = (,) <$> Gen.int (Range.linear 0 10_000_000) <*> Gen.int (Range.linear 0 10_000_000)

genXReadStreamId :: Gen XReadStreamId
genXReadStreamId = Gen.choice [pure Dollar, Concrete <$> genConcreteStreamId]

genXRange :: Gen XRange
genXRange = do
    start <- Gen.choice [pure XMinus, XS <$> genStreamId']
    end <- Gen.choice [pure XPlus, XE <$> genStreamId']
    pure (MkXrange start end)
  where
    genStreamId' =
        (,)
            <$> Gen.int (Range.linear 0 10_000_000)
            <*> Gen.choice [pure Nothing, Just <$> Gen.int (Range.linear 0 10_000_000)]

genXAddStreamId :: Gen XAddStreamId
genXAddStreamId =
    Gen.choice
        [ pure AutoId
        , ExplicitId <$> genTs <*> genSeq
        ]
  where
    genTs = Gen.int (Range.linear 0 10_000_000)
    genSeq =
        Gen.choice
            [ pure SeqAuto
            , Seq <$> Gen.int (Range.linear 0 10_000_000)
            ]
