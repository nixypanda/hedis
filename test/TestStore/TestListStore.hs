{-# LANGUAGE OverloadedStrings #-}

module TestStore.TestListStore (tests) where

import Control.Concurrent.Async
import Control.Concurrent.STM
import Data.ByteString (ByteString)
import Data.ByteString.Char8 qualified as BS
import Hedgehog
import Hedgehog.Gen qualified as Gen
import Hedgehog.Range qualified as Range
import System.Timeout (timeout)
import Test.Tasty (TestTree, testGroup)
import Test.Tasty.Hedgehog (testProperty)

import Data.Maybe (listToMaybe)
import Store.ListStore
import StoreBackend.ListMap (Range (MkRange))

tests :: TestTree
tests =
    testGroup
        "ListStore"
        [ testProperty "prop_rpush_length" prop_rpush_length
        , testProperty "prop_rpush_order" prop_rpushPreservesOrder
        , testProperty "prop_lpush_order" prop_lpushReversesOrder
        , testProperty "prop_lpop_behavior" prop_lpop
        , testProperty "prop_lpops" prop_lpops
        , testProperty "prop_blpop_unblocks" prop_blpopBlocksAndUnblocks
        ]

genByteString :: Gen ByteString
genByteString =
    BS.pack <$> Gen.list (Range.linear 0 10) (Gen.element ['a' .. 'z'])

genNonEmptyList :: Gen [ByteString]
genNonEmptyList =
    Gen.list (Range.linear 1 50) genByteString

-- RPUSH length invariant
prop_rpush_length :: Property
prop_rpush_length = property $ do
    xs <- forAll genNonEmptyList

    tv <- evalIO $ atomically emptySTM
    _ <- evalIO $ atomically $ rpushSTM tv "k" xs
    n <- evalIO $ atomically $ llenSTM tv "k"

    n === length xs

prop_lpushReversesOrder :: Property
prop_lpushReversesOrder = property $ do
    xs <- forAll genNonEmptyList

    tv <- evalIO $ atomically emptySTM
    _ <- evalIO $ atomically $ lpushSTM tv "k" xs
    ys <- evalIO $ atomically $ lrangeSTM tv "k" $ MkRange 0 (-1)

    ys === reverse xs

prop_rpushPreservesOrder :: Property
prop_rpushPreservesOrder = property $ do
    xs <- forAll genNonEmptyList

    tv <- evalIO $ atomically emptySTM
    _ <- evalIO $ atomically $ rpushSTM tv "k" xs
    ys <- evalIO $ atomically $ lrangeSTM tv "k" $ MkRange 0 (-1)

    ys === xs

prop_lpop :: Property
prop_lpop = property $ do
    xs <- forAll genNonEmptyList

    tv <- evalIO $ atomically emptySTM
    _ <- evalIO $ atomically $ rpushSTM tv "k" xs
    v <- evalIO $ atomically $ lpopSTM tv "k"
    ys <- evalIO $ atomically $ lrangeSTM tv "k" $ MkRange 0 (-1)

    v === listToMaybe xs
    ys === drop 1 xs

prop_lpops :: Property
prop_lpops = property $ do
    xs <- forAll genNonEmptyList
    n <- forAll $ Gen.int (Range.linear 1 (length xs))

    tv <- evalIO $ atomically emptySTM
    _ <- evalIO $ atomically $ rpushSTM tv "k" xs
    ys <- evalIO $ atomically $ lpopsSTM tv "k" n
    zs <- evalIO $ atomically $ lrangeSTM tv "k" $ MkRange 0 (-1)

    ys === take n xs
    zs === drop n xs

prop_blpopBlocksAndUnblocks :: Property
prop_blpopBlocksAndUnblocks = property $ do
    x <- forAll genByteString

    tv <- evalIO $ atomically emptySTM

    a <- evalIO $ async $ atomically $ blpopSTM tv "k"

    -- ensure it is blocking
    r1 <- evalIO $ timeout 2000 (wait a)
    r1 === Nothing

    -- unblock
    _ <- evalIO $ atomically $ rpushSTM tv "k" [x]
    r2 <- evalIO $ wait a

    r2 === ["k", x]
