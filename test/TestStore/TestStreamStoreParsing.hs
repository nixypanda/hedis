{-# LANGUAGE OverloadedStrings #-}

module TestStore.TestStreamStoreParsing (tests) where

import Hedgehog (Property, annotate, failure, forAll, property, (===))
import Test.Tasty (TestTree, testGroup)
import Test.Tasty.Hedgehog (testProperty)

import Test.Tasty.HUnit (testCase, (@?=))

import Gen.Stream (genConcreteStreamId, genXAddStreamId, genXRange)
import Store.StreamStoreParsing
import StoreBackend.StreamMap

tests :: TestTree
tests =
    testGroup
        "StreamStoreParsing"
        [ streamIdTests
        , testProperty "readXAddStreamId <-> showXaddId" prop_XAddRoundtrip
        , testProperty "readXRange <-> showXRange" prop_XRangeRoundtrip
        , testProperty "readConcreteStreamId <-> showStreamId" prop_ConcreteStreamIdRoundtrip
        ]

streamIdTests :: TestTree
streamIdTests =
    testGroup
        "StreamId parser"
        [ testCase "*" $ readXAddStreamId "*" @?= Right AutoId
        , testCase "0-1" $ readXAddStreamId "0-1" @?= Right (ExplicitId 0 (Seq 1))
        , testCase "1526919030474-0" $ readXAddStreamId "1526919030474-0" @?= Right (ExplicitId 1526919030474 (Seq 0))
        , testCase "1-*" $ readXAddStreamId "1-*" @?= Right (ExplicitId 1 SeqAuto)
        , testCase "1526919030474-*" $ readXAddStreamId "1526919030474-*" @?= Right (ExplicitId 1526919030474 SeqAuto)
        ]

prop_XAddRoundtrip :: Property
prop_XAddRoundtrip = property $ do
    sid <- forAll genXAddStreamId
    let bs = showXaddId sid

    case readXAddStreamId bs of
        Right sid' -> sid' === sid
        Left err -> do
            annotate err
            failure

prop_XRangeRoundtrip :: Property
prop_XRangeRoundtrip = property $ do
    xr <- forAll genXRange
    let (s, e) = showXRange xr

    case readXRange s e of
        Right xr' -> xr' === xr
        Left err -> do
            annotate err
            failure

prop_ConcreteStreamIdRoundtrip :: Property
prop_ConcreteStreamIdRoundtrip = property $ do
    sid <- forAll genConcreteStreamId
    let bs = showStreamId sid

    case readConcreteStreamId bs of
        Right sid' -> sid' === sid
        Left err -> do
            annotate err
            failure
