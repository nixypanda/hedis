{-# LANGUAGE OverloadedStrings #-}

module TestParsers where

import Hedgehog (Property, forAll, property, (===))
import Hedgehog.Gen qualified as Gen
import Hedgehog.Range qualified as Range
import Test.Tasty (TestTree, testGroup)
import Test.Tasty.Hedgehog (testProperty)

import Data.String (fromString)
import Test.Tasty.HUnit (testCase, (@?=))

import Parsers (readFloatBS, readIntBS, readStreamId)
import Store.StreamMap

tests :: TestTree
tests =
    testGroup
        "Parser"
        [ testProperty "signedIntParser parses generated integers" prop_signedInt
        , testProperty "signedFloatParser parses generated floats" prop_signedFloat
        , streamIdTests
        ]

prop_signedInt :: Property
prop_signedInt =
    property $ do
        n <- forAll $ Gen.int (Range.linear (-100000) 100000)
        let s = if n >= 0 then show n else '-' : show (abs n)
        let result = readIntBS (fromString s)
        result === Right n

prop_signedFloat :: Property
prop_signedFloat =
    property $ do
        f <- forAll $ Gen.float (Range.linearFrac (-1e5) 1e5)
        let s = show f
        let result = readFloatBS (fromString s)
        result === Right f

streamIdTests :: TestTree
streamIdTests =
    testGroup
        "StreamId parser"
        [ testCase "*" $ readStreamId "*" @?= Right AutoId
        , testCase "0-1" $ readStreamId "0-1" @?= Right (ExplicitId 0 (Seq 1))
        , testCase "1526919030474-0" $ readStreamId "1526919030474-0" @?= Right (ExplicitId 1526919030474 (Seq 0))
        , testCase "1-*" $ readStreamId "1-*" @?= Right (ExplicitId 1 SeqAuto)
        , testCase "1526919030474-*" $ readStreamId "1526919030474-*" @?= Right (ExplicitId 1526919030474 SeqAuto)
        ]
