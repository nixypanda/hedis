{-# OPTIONS_GHC -Wno-incomplete-uni-patterns #-}

module TestStoreBackend.TestStreamMap (tests) where

import Hedgehog (Property, forAll, property, (===))
import Test.Tasty (TestTree, testGroup)
import Test.Tasty.HUnit (Assertion, assertFailure, testCase, (@?=))
import Test.Tasty.Hedgehog (testProperty)

import Data.List (sort)
import Data.Time (UTCTime)
import Data.Time.Clock.POSIX (posixSecondsToUTCTime)

import Gen.Stream (genInsertSeq)
import StoreBackend.StreamMap

val :: ConcreteStreamId -> [(Int, Int)] -> Value Int Int
val = MkValue

streamVals :: [Value Int Int]
streamVals =
    [ val (1, 0) [(1, 1)]
    , val (1, 1) [(2, 2)]
    , val (2, 0) [(3, 3)]
    , val (2, 1) [(4, 4)]
    ]

streamMap :: StreamMap String Int Int
streamMap =
    fromList
        [ ("a", streamVals)
        ]

tests :: TestTree
tests = testGroup "StreamMap" [testsQuery, testsInsert]

testsQuery :: TestTree
testsQuery =
    testGroup
        "StreamMap.query"
        [ testCase "missing key returns empty" $
            query "missing" fullRange streamMap @?= []
        , testCase "empty map returns empty" $
            query "a" fullRange (empty :: StreamMap String Int Int) @?= []
        , testCase "full range returns all values" $
            query "a" fullRange streamMap @?= streamVals
        , testCase "range starting from timestamp only" $
            query "a" (MkXrange (XS (2, Nothing)) XPlus) streamMap
                @?= drop 2 streamVals
        , testCase "range ending at timestamp only" $
            query "a" (MkXrange XMinus (XE (1, Nothing))) streamMap
                @?= take 2 streamVals
        , testCase "exact id range" $
            query "a" (MkXrange (XS (1, Just 1)) (XE (1, Just 1))) streamMap
                @?= [streamVals !! 1]
        , testCase "inclusive lower bound" $
            query "a" (MkXrange (XS (1, Just 0)) XPlus) streamMap
                @?= streamVals
        , testCase "inclusive upper bound" $
            query "a" (MkXrange XMinus (XE (2, Just 0))) streamMap
                @?= take 3 streamVals
        , testCase "range inside stream" $
            query "a" (MkXrange (XS (1, Just 1)) (XE (2, Just 0))) streamMap
                @?= [streamVals !! 1, streamVals !! 2]
        , testCase "range with no matching elements" $
            query "a" (MkXrange (XS (3, Nothing)) XPlus) streamMap
                @?= []
        ]

t1, t2 :: UTCTime
t1 = posixSecondsToUTCTime 1 -- 1s → 1000 milliseconds
t2 = posixSecondsToUTCTime 2 -- 2s → 2000 milliseconds

assertRight :: Either a b -> (b -> Assertion) -> Assertion
assertRight (Right x) f = f x
assertRight (Left _) _ = assertFailure "Expected Right"

assertLeft :: (Eq a, Show a) => Either a b -> a -> Assertion
assertLeft (Left x) e = x @?= e
assertLeft (Right _) _ = assertFailure "Expected Left"

oneOne :: (Int, Int)
oneOne = (1, 1)

testsInsert :: TestTree
testsInsert =
    testGroup
        "StreamMap.insert"
        [ testCase "insert into empty map with AutoId" $
            insert "a" AutoId [oneOne] t1 empty
                `assertRight` \(sid, m) -> do
                    sid @?= (1000, 0)
                    length (m ! "a") @?= 1
        , testCase "insert preserves existing values" $
            let Right (_, m1) = insert "a" AutoId [oneOne] t1 empty
                Right (_, m2) = insert "a" AutoId [(2, 2)] t1 m1
             in do
                    length (m2 ! "a") @?= 2
        , testCase "AutoId increments seq on same timestamp" $
            let Right (_, m1) = insert "a" AutoId [oneOne] t1 empty
                Right (sid, _) = insert "a" AutoId [(2, 2)] t1 m1
             in sid @?= (1000, 1)
        , testCase "AutoId resets seq on new timestamp" $
            let Right (_, m1) = insert "a" AutoId [oneOne] t1 empty
                Right (sid, _) = insert "a" AutoId [(2, 2)] t2 m1
             in sid @?= (2000, 0)
        , testCase "ExplicitId with Seq inserts exact id" $
            insert "a" (ExplicitId 10 (Seq 5)) [oneOne] t1 empty
                `assertRight` \(sid, _) ->
                    sid @?= (10, 5)
        , testCase "ExplicitId SeqAuto increments when timestamp matches" $
            let Right (_, m1) = insert "a" (ExplicitId 10 (Seq 1)) [oneOne] t1 empty
                Right (sid, _) = insert "a" (ExplicitId 10 SeqAuto) [(2, 2)] t1 m1
             in sid @?= (10, 2)
        , testCase "ExplicitId SeqAuto resets when timestamp differs" $
            let Right (_, m1) = insert "a" (ExplicitId 10 (Seq 7)) [oneOne] t1 empty
                Right (sid, _) = insert "a" (ExplicitId 11 SeqAuto) [(2, 2)] t1 m1
             in sid @?= (11, 0)
        , testCase "BaseStreamId is rejected" $
            insert "a" (ExplicitId 0 (Seq 0)) [oneOne] t1 empty
                `assertLeft` BaseStreamId
        , testCase "NotLargerId is rejected" $
            let Right (_, m1) = insert "a" (ExplicitId 10 (Seq 1)) [oneOne] t1 empty
             in insert "a" (ExplicitId 10 (Seq 1)) [(2, 2)] t1 m1
                    `assertLeft` NotLargerId
        , testCase "NotLargerId rejected when seq decreases" $
            let Right (_, m1) = insert "a" (ExplicitId 10 (Seq 5)) [oneOne] t1 empty
             in insert "a" (ExplicitId 10 (Seq 3)) [(2, 2)] t1 m1
                    `assertLeft` NotLargerId
        , testCase "inserting into different keys is independent" $
            let Right (_, m1) = insert "a" AutoId [oneOne] t1 empty
                Right (_, m2) = insert "b" AutoId [(2, 2)] t1 m1
             in do
                    length (m2 ! "a") @?= 1
                    length (m2 ! "b") @?= 1
        , testProperty "ids are strictly increasing" prop_idsStrictlyIncreasing
        ]

fullRange :: XRange
fullRange = MkXrange XMinus XPlus

-- Property

prop_idsStrictlyIncreasing :: Property
prop_idsStrictlyIncreasing =
    property $ do
        ops <- forAll genInsertSeq
        let
            go _ [] acc = reverse acc
            go m (sid : xs) acc =
                case insert "k" sid [] t1 m of
                    Left _ -> go m xs acc
                    Right (cid, m') -> go m' xs (cid : acc)

        let ids = go empty ops []
        ids === sort ids
