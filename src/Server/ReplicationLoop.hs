{-# LANGUAGE GADTs #-}
{-# LANGUAGE OverloadedStrings #-}

module Server.ReplicationLoop (runReplication) where

import Control.Concurrent.STM (atomically, modifyTVar)
import Control.Monad (forever)
import Control.Monad.Except (liftEither)
import Control.Monad.IO.Class (MonadIO (liftIO))
import Control.Monad.Reader (MonadReader (ask))
import Data.Bifunctor (Bifunctor (first))
import Data.Time (getCurrentTime)
import Network.Simple.TCP (Socket)

import Execution.Base (runCmdSTM)
import Protocol.MasterCmd (MasterCommand (..), propogationCmdToCmdSTM)
import Protocol.Result
import Replication.Master (runMasterToReplicaReplicationCmds)
import Replication.Replica (doHandshake)
import Resp.Client (RespConn, mkRespConn, recvRdb, recvResp, sendResp)
import Types.Redis
import Wire.Class (FromResp (..), ToResp (..))

runReplication :: Socket -> Redis Replica ()
runReplication socket = do
    EnvReplica _ state <- ask
    respConn <- liftIO $ mkRespConn socket
    doHandshake state respConn

    logInfo $ "Handshake complete. Waiting for RDB" <> show socket
    _rdb <- liftIO $ recvRdb respConn

    receiveMasterUpdates respConn

receiveMasterUpdates :: RespConn -> Redis Replica ()
receiveMasterUpdates respConn = do
    forever $ do
        env <- ask
        resp <- liftIO $ recvResp respConn
        command <- liftEither $ first ConversionError $ fromResp resp
        now <- liftIO getCurrentTime
        let offsetIncr = modifyTVar (getOffset env) (+ respBytes command)
        case command of
            PropogationCmd c -> liftIO $ atomically $ do
                _ <- runCmdSTM env now . propogationCmdToCmdSTM $ c
                offsetIncr
            ReplicationCommand c -> do
                res <- liftIO $ atomically $ do
                    res <- runMasterToReplicaReplicationCmds env c
                    offsetIncr
                    pure $ ResNormal res
                liftIO $ sendResp respConn $ toResp res
            MasterPing -> do
                liftIO $ atomically offsetIncr
