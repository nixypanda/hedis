{-# LANGUAGE OverloadedStrings #-}

module Server.ClientLoop (clientLoopWrite) where

import Control.Concurrent.STM (STM, atomically, modifyTVar, readTVar, writeTVar)
import Control.Monad (forM_, forever)
import Control.Monad.Except (liftEither)
import Control.Monad.IO.Class (MonadIO (liftIO))
import Control.Monad.Reader (MonadReader (ask))
import Data.Bifunctor (Bifunctor (first))
import Data.ByteString (ByteString)
import Data.List (delete, nub)
import Data.Time (UTCTime, getCurrentTime)
import Network.Simple.TCP (Socket, send)

import Execution.Base (runConfigInfoCmds, runServerInfoCmds)
import Protocol.Command
import Protocol.Message (Message (MkMessage))
import Protocol.Result
import Replication.Master (runAndReplicateIO, runAndReplicateSTM, runMasterToReplicaReplicationCmds, sendReplConfs)
import Replication.Replica (runReplicaToMasterReplicationCmds)
import Resp.Client (mkRespConn, recvResp)
import Resp.Core (encode)
import Store.PubSubStore (getChannels)
import Store.PubSubStore qualified as PS
import Store.TypeStore qualified as TS
import StoreBackend.TypeIndex (ValueType (..))
import Types.Redis
import Wire.Class (FromResp (..), ToResp (..))

--- client server

clientLoopWrite :: (HasLogger r, HasStores r, HasReplication r) => ClientState -> Socket -> Redis r ()
clientLoopWrite clientState socket = do
    respConn <- liftIO $ mkRespConn socket
    forever $ do
        resp <- liftIO $ recvResp respConn
        cmd <- liftEither $ first RespParsingError $ fromResp resp
        result <- runCmd clientState cmd
        case result of
            ResNothing -> pure ()
            res -> do
                let encoded = encode $ toResp res
                liftIO $ send socket encoded

-- Common Command Execution

runCmd :: (HasStores r, HasReplication r, HasLogger r) => ClientState -> Command -> Redis r Result
runCmd clientState command = do
    env <- ask
    now <- liftIO getCurrentTime
    (txState, subbedChannels) <- liftIO $ atomically $ do
        ts <- readTVar clientState.txState
        sc <- readTVar clientState.subbedChannels
        pure (ts, sc)
    case subbedChannels of
        [] -> case command of
            RedRepl (CmdReplicaToMaster c) -> resFromMaybe <$> runReplicaToMasterReplicationCmds clientState.socket c
            RedRepl (CmdMasterToReplica c) -> liftIO . atomically $ ResNormal <$> runMasterToReplicaReplicationCmds env c
            RedInfo section -> liftIO . atomically $ ResNormal <$> runServerInfoCmds env section
            RedConfig section -> pure . ResNormal $ runConfigInfoCmds env section
            RedIO cmd' -> ResNormal <$> runAndReplicateIO env cmd'
            RedSub cmd' -> ResNormal <$> handlePubSubMessage clientState cmd'
            CmdWait n tout -> ResNormal <$> sendReplConfs clientState n tout
            cmd -> case txState of
                InTx cs ->
                    case cmd of
                        RedSTM c -> liftIO $ atomically $ do
                            writeTVar clientState.txState (InTx $ c : cs)
                            pure . ResNormal $ RSimple "QUEUED"
                        RedTrans m -> case m of
                            Multi -> pure . ResError $ RTxErr RMultiInMulti
                            Exec -> ResCombined <$> liftIO (atomically $ executeQueuedCmds clientState env now cs)
                            Discard -> liftIO $ atomically $ do
                                writeTVar clientState.txState NoTx
                                pure . ResNormal $ ResOk
                NoTx -> case cmd of
                    RedSTM cmd' -> resFromEither <$> liftIO (atomically $ runAndReplicateSTM env now cmd')
                    RedTrans txCmd ->
                        case txCmd of
                            Exec -> pure . ResError $ RTxErr RExecWithoutMulti
                            Discard -> pure . ResError $ RTxErr RDiscardWithoutMulti
                            Multi -> liftIO $ atomically $ do
                                modifyTVar clientState.txState (const $ InTx [])
                                pure . ResNormal $ ResOk
        _ -> case command of
            RedSTM CmdPing -> pure $ ResNormal ResPongSubscribed
            RedSub cmd' -> ResNormal <$> handlePubSubMessage clientState cmd'
            cmd -> pure $ ResError $ RCmdNotAllowedInMode cmd ModeSubscribed

-- pub-sub

handlePubSubMessage :: (HasStores r) => ClientState -> PubSub -> Redis r CommandResult
handlePubSubMessage clientState command = do
    env <- ask
    case command of
        CmdSubscribe chan -> liftIO $ atomically $ subscribeToChannel clientState env chan
        CmdPublish chan msg -> liftIO $ publishToChannel env chan msg
        CmdUnsubscribe chan -> liftIO $ atomically $ unsubscribeFromChannel clientState env chan

publishToChannel :: (HasStores r) => Env r -> Key -> ByteString -> IO CommandResult
publishToChannel env key msg = do
    channels <- liftIO $ atomically $ getChannels key (getPubSubStore env)
    forM_ channels $ \chan -> do
        let msg' = MkMessage key msg
        send chan (encode $ toResp msg')
    pure $ RInt (length channels)

subscribeToChannel :: (HasStores r) => ClientState -> Env r -> Key -> STM CommandResult
subscribeToChannel clientState env chan = do
    let tvPubSubStore = getPubSubStore env
        tvTypeIndex = getTypeIndex env
    _ <- TS.setIfAvailable tvTypeIndex chan VChannel *> PS.addChannel chan clientState.socket tvPubSubStore
    modifyTVar clientState.subbedChannels (nub . (chan :))
    chans <- readTVar clientState.subbedChannels
    pure $ ResSubscribed chan (length chans)

unsubscribeFromChannel :: (HasStores r) => ClientState -> Env r -> Key -> STM CommandResult
unsubscribeFromChannel clientState env chan = do
    let tvPubSubStore = getPubSubStore env
    PS.removeChannel chan clientState.socket tvPubSubStore
    modifyTVar clientState.subbedChannels (delete chan)
    chans <- readTVar clientState.subbedChannels
    pure $ ResUnsubscribed chan (length chans)

-- Transaction

executeQueuedCmds :: (HasStores r, HasReplication r) => ClientState -> Env r -> UTCTime -> [CmdSTM] -> STM [Either CommandError CommandResult]
executeQueuedCmds clientState env now cmds = do
    vals <- mapM (runAndReplicateSTM env now) (reverse cmds)
    modifyTVar clientState.txState (const NoTx)
    pure vals
