module Resp.Client (RespConn, mkRespConn, recvRdb, recvResp, sendResp) where

import Control.Concurrent.STM (TVar, atomically, modifyTVar', newTVarIO, readTVarIO, writeTVar)
import Control.Exception (Exception, IOException, throwIO)
import Data.ByteString (ByteString)
import Network.Simple.TCP (Socket, recv, send)
import Text.Parsec.ByteString (Parser)

import Parsers (parseWithRemainder)
import RDB (RDBEncoded, rdbParser)
import Resp.Core (Resp, encode, resp)

data RespError
    = RespEOF
    | RespParseError String
    | RespIOError IOException
    deriving (Show)

instance Exception RespError

data RespConn = RespConn
    { rcSocket :: Socket
    , rcBuffer :: TVar ByteString
    }

sendResp :: RespConn -> Resp -> IO ()
sendResp RespConn{..} r =
    send rcSocket (encode r)

recv' :: Parser a -> RespConn -> IO a
recv' parser conn@RespConn{..} = do
    buf <- readTVarIO rcBuffer
    case parseWithRemainder parser buf of
        Right (respP, rest) -> do
            atomically $ writeTVar rcBuffer rest
            pure respP
        Left _ -> do
            mbs <- recv rcSocket 4096
            case mbs of
                Nothing -> throwIO RespEOF
                Just bs -> do
                    atomically $ modifyTVar' rcBuffer (<> bs)
                    recv' parser conn

recvResp :: RespConn -> IO Resp
recvResp = recv' resp

recvRdb :: RespConn -> IO RDBEncoded
recvRdb = recv' rdbParser

mkRespConn :: Socket -> IO RespConn
mkRespConn sock = do
    buf <- newTVarIO mempty
    pure $ RespConn sock buf
