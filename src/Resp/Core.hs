{-# LANGUAGE OverloadedStrings #-}

module Resp.Core (
    Resp (..),
    encode,
    decode,
    decodeMany,
    -- testing
    respsWithRemainder,
    respParser,
    resps,
) where

import Control.Monad (replicateM)
import Data.ByteString (ByteString)
import Data.ByteString qualified as BS
import Data.String (fromString)
import Text.Parsec (ParseError, anyChar, count, crlf, getInput, many, parse, try)
import Text.Parsec.ByteString (Parser)

import Parsers (bytes, signedIntParser)

-- RESP

data Resp
    = Str ByteString
    | StrErr ByteString
    | Array Int [Resp]
    | BulkStr ByteString
    | Int Int
    | NullBulk
    | NullArray
    deriving (Show, Eq)

-- parsing/decoding

respsWithRemainder :: Parser ([Resp], ByteString)
respsWithRemainder = do
    rs <- resps
    rest <- getInput
    pure (rs, rest)

resps :: Parser [Resp]
resps = many (try respParser)

respParser :: Parser Resp
respParser = do
    t <- anyChar
    case t of
        '+' -> Str <$> bytes <* crlf
        '-' -> StrErr <$> bytes <* crlf
        '*' -> array
        ':' -> Int <$> signedIntParser <* crlf
        '$' -> bulkString
        _ -> fail $ "invalid type tag: " ++ show t

array :: Parser Resp
array = do
    n <- signedIntParser <* crlf
    if
        | n >= 0 -> Array n <$> replicateM n respParser
        | n == -1 -> pure NullArray
        | otherwise -> fail "negative array length"

bulkString :: Parser Resp
bulkString = do
    n <- signedIntParser <* crlf
    if
        | n >= 0 -> BulkStr . fromString <$> count n anyChar <* crlf
        | n == -1 -> pure NullBulk
        | otherwise -> fail "negative bulk length"

decode :: ByteString -> Either ParseError Resp
decode = parse respParser ""

decodeMany :: ByteString -> Either ParseError ([Resp], ByteString)
decodeMany = parse respsWithRemainder "<redis-stream>"

-- encoding

crlf' :: ByteString
crlf' = "\r\n"

encode :: Resp -> ByteString
encode (Str x) = "+" <> x <> crlf'
encode (StrErr x) = "-" <> x <> crlf'
encode (BulkStr x) = "$" <> fromString (show $ BS.length x) <> crlf' <> x <> crlf'
encode (Int x) = ":" <> fromString (show x) <> crlf'
encode (Array n rs) = "*" <> fromString (show n) <> crlf' <> BS.concat (map encode rs)
encode NullBulk = "$-1\r\n"
encode NullArray = "*-1\r\n"
