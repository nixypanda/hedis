{-# LANGUAGE DataKinds #-}
{-# LANGUAGE DeriveAnyClass #-}
{-# LANGUAGE DerivingStrategies #-}
{-# LANGUAGE GADTs #-}
{-# LANGUAGE KindSignatures #-}
{-# LANGUAGE OverloadedStrings #-}

module Types.Redis (
    HasReplication (..),
    HasLogger (..),
    HasStores (..),
    Redis (..),
    ClientState (..),
    Env (..),
    Replica,
    Master,
    TxState (..),
    HandshakeError (InvalidReturn),
    RedisError (..),
    Stores (..),
    CommonEnv (..),
    mkMasterEnv,
    mkReplicaEnv,
    logInfo,
    newClient,
) where

import Control.Concurrent.STM (TVar, atomically, newTVar)
import Control.Exception (Exception)
import Control.Monad.Except (ExceptT (..), MonadError)
import Control.Monad.IO.Class (MonadIO (liftIO))
import Control.Monad.Reader (MonadReader, ReaderT, asks)
import Data.Kind (Type)
import Network.Simple.TCP (Socket)
import System.Log.FastLogger (
    LogType' (..),
    TimedFastLogger,
    ToLogStr (..),
    defaultBufSize,
    newTimeCache,
    newTimedFastLogger,
    simpleTimeFormat,
 )

import Control.Monad.STM (STM)
import Protocol.Command
import Protocol.Result
import Replication.Config (
    MasterConfig,
    MasterState (..),
    ReplicaConfig,
    ReplicaState (..),
    Replication (..),
    initMasterState,
    initReplicaState,
 )
import Store.AuthStore (AuthStore)
import Store.AuthStore qualified as AS
import Store.ListStore (ListStore)
import Store.ListStore qualified as LS
import Store.PubSubStore (PubSubStore)
import Store.PubSubStore qualified as PS
import Store.SortedSetStore (SortedSetStore)
import Store.SortedSetStore qualified as SSS
import Store.StreamStore (StreamStore)
import Store.StreamStore qualified as StS
import Store.StringStore (StringStore)
import Store.StringStore qualified as SS
import Store.TypeStore (TypeIndex)
import Store.TypeStore qualified as TS

-- Types

data RedisError
    = RdbParsingError String
    | RespParsingError String
    | HandshakeError HandshakeError
    | EmptyBuffer
    | IOError String
    deriving (Show, Exception)

data HandshakeError
    = InvalidReturn ReplicaToMaster Success Result
    deriving (Show, Eq)

-- Transactions

data TxState = NoTx | InTx [CmdSTM] -- queue only STM-capable commands
    deriving (Show)

-- Env

data Stores = MkStores
    { stringStore :: TVar StringStore
    , listStore :: TVar ListStore
    , typeIndex :: TVar TypeIndex
    , streamStore :: TVar StreamStore
    , pubSubStore :: TVar PubSubStore
    , sortedSetStore :: TVar SortedSetStore
    , authStore :: TVar AuthStore
    }

data Master
data Replica

data Env (r :: Type) where
    EnvMaster :: CommonEnv -> MasterState -> Env Master
    EnvReplica :: CommonEnv -> ReplicaState -> Env Replica

data CommonEnv = MkCommonEnv
    { stores :: Stores
    , envLogger :: TimedFastLogger
    }

mkCommonEnv :: IO CommonEnv
mkCommonEnv = do
    stores <- atomically $ do
        stringStore <- SS.emptySTM
        listStore <- LS.emptySTM
        typeIndex <- TS.emptySTM
        streamStore <- StS.emptySTM
        pubSubStore <- PS.emptySTM
        sortedSetStore <- SSS.emptySTM
        authStore <- AS.emptySTM
        pure $ MkStores{..}

    timeCache <- newTimeCache simpleTimeFormat
    (envLogger, _cleanup) <- newTimedFastLogger timeCache (LogStderr defaultBufSize)
    pure $ MkCommonEnv stores envLogger

mkMasterEnv :: MasterConfig -> IO (Env Master)
mkMasterEnv mc = EnvMaster <$> mkCommonEnv <*> atomically (initMasterState mc)

mkReplicaEnv :: ReplicaConfig -> IO (Env Replica)
mkReplicaEnv rc = EnvReplica <$> mkCommonEnv <*> atomically (initReplicaState rc)

data ClientState = MkClientState
    { txState :: TVar TxState
    , socket :: Socket
    , subbedChannels :: TVar [Key]
    , name :: TVar Key
    , authenticated :: TVar Bool
    }

newClient :: Socket -> STM ClientState
newClient socket = do
    txState <- newTVar NoTx
    subbedChannels <- newTVar []
    name <- newTVar "default"
    authenticated <- newTVar False
    pure MkClientState{..}

newtype Redis r a = MkRedis {runRedis :: ReaderT (Env r) (ExceptT RedisError IO) a}
    deriving newtype (Functor, Applicative, Monad, MonadError RedisError, MonadIO, MonadReader (Env r), MonadFail)

class HasStores r where
    getStores :: Env r -> Stores

    getListStore :: Env r -> TVar ListStore
    getListStore = listStore . getStores

    getTypeIndex :: Env r -> TVar TypeIndex
    getTypeIndex = typeIndex . getStores

    getStringStore :: Env r -> TVar StringStore
    getStringStore = stringStore . getStores

    getStreamStore :: Env r -> TVar StreamStore
    getStreamStore = streamStore . getStores

    getPubSubStore :: Env r -> TVar PubSubStore
    getPubSubStore = pubSubStore . getStores

    getSortedSetStore :: Env r -> TVar SortedSetStore
    getSortedSetStore = sortedSetStore . getStores

    getAuthStore :: Env r -> TVar AuthStore
    getAuthStore = authStore . getStores

instance HasStores Master where
    getStores :: Env Master -> Stores
    getStores (EnvMaster c _) = c.stores

instance HasStores Replica where
    getStores :: Env Replica -> Stores
    getStores (EnvReplica c _) = c.stores

class HasLogger r where
    getLogger :: Env r -> TimedFastLogger

instance HasLogger Master where
    getLogger :: Env Master -> TimedFastLogger
    getLogger (EnvMaster c _) = c.envLogger

instance HasLogger Replica where
    getLogger :: Env Replica -> TimedFastLogger
    getLogger (EnvReplica c _) = c.envLogger

class HasReplication r where
    getReplication :: Env r -> Replication
    getOffset :: Env r -> TVar Int

instance HasReplication Master where
    getReplication :: Env Master -> Replication
    getReplication (EnvMaster _ ms) = MkReplicationMaster ms

    getOffset (EnvMaster _ ms) = ms.masterReplOffset

instance HasReplication Replica where
    getReplication :: Env Replica -> Replication
    getReplication (EnvReplica _ rs) = MkReplicationReplica rs

    getOffset (EnvReplica _ rs) = rs.replicaOffset

logInfo :: (MonadReader (Env r) m, MonadIO m, HasLogger r, HasReplication r) => String -> m ()
logInfo msg = do
    logger <- asks getLogger
    repInfo <- asks getReplication
    let prefix = case repInfo of
            MkReplicationMaster _ -> "[ MASTER ]"
            MkReplicationReplica _ -> "[ REPLICA ]"

    liftIO $ logger (\t -> toLogStr (show t <> prefix <> "[INFO] " <> msg <> "\n"))
