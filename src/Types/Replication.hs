{-# LANGUAGE DeriveAnyClass #-}
{-# LANGUAGE DuplicateRecordFields #-}
{-# LANGUAGE OverloadedStrings #-}

module Types.Replication (
    Replication (..),
    ReplicaOf (..),
    ReplicationConfig (..),
    MasterConfig (..),
    ReplicaConfig (..),
    MasterState (..),
    ReplicaState (..),
    ReplicaConn (..),
    RdbConfig (..),
    ReplicaRegistry,
    HasRdbConfig (..),
    replicationInfo,
    initMasterState,
    initReplicaState,
) where

import Control.Concurrent.Async (Async)
import Control.Concurrent.STM (STM, TQueue, TVar, newTVar, readTVar)
import Control.Exception (Exception)
import Data.ByteString (ByteString)
import Data.ByteString qualified as BS
import Data.String (fromString)
import Network.Simple.TCP (Socket)

-- CLI flags ──▶ ReplicationConfig
--                 │
--                 ▼
--           Replication (Env)
--            /           \
--   MasterState       ReplicaState
--        │                  │
--     INFO               PSYNC / REPLCONF

-- Used in CLI parsing
data ReplicaOf = MkReplicaOf
    { masterHost :: String
    , masterPort :: Int
    }
    deriving (Show, Eq)

data RdbConfig = MkRdbConfig
    { dir :: String
    , dbfilename :: String
    }
    deriving (Show, Eq)

class HasRdbConfig a where
    rdbFilePath :: a -> FilePath
    rdbDir :: a -> FilePath
    rdbFilename :: a -> FilePath

instance HasRdbConfig MasterState where
    rdbFilePath a = _rdbFilePath a.rdbConfig
    rdbDir a = a.rdbConfig.dir
    rdbFilename a = a.rdbConfig.dbfilename

instance HasRdbConfig ReplicaState where
    rdbFilePath a = _rdbFilePath a.rdbConfig
    rdbDir a = a.rdbConfig.dir
    rdbFilename a = a.rdbConfig.dbfilename

_rdbFilePath :: RdbConfig -> FilePath
_rdbFilePath MkRdbConfig{..} = dir <> "/" <> dbfilename

-- ReplicationConfig is stuff read from CLI
data ReplicationConfig = RCMaster MasterConfig | RCReplica ReplicaConfig
    deriving (Show)

newtype MasterConfig = MkMasterConfig {rdbConfig :: RdbConfig}
    deriving (Show)

data ReplicaConfig = MkReplicaConfig {masterInfo :: ReplicaOf, localPort :: Int, rdbConfig :: RdbConfig}
    deriving (Show)

-- Replication is what we store in the Env
data Replication
    = MkReplicationMaster MasterState
    | MkReplicationReplica ReplicaState

data MasterState = MkMasterState
    { masterReplId :: ByteString
    , masterReplOffset :: TVar Int
    , replicaRegistry :: ReplicaRegistry
    , rdbConfig :: RdbConfig
    }

data ReplicaState = MkReplicaState
    { masterInfo :: ReplicaOf
    , knownMasterRepl :: TVar ByteString
    , replicaOffset :: TVar Int
    , localPort :: Int
    , rdbConfig :: RdbConfig
    }

data ReplicaConn = MkReplicaConn
    { rcSocket :: Socket
    , rcOffset :: TVar Int
    , rcAuxCmdOffset :: TVar Int
    , rcQueue :: TQueue ByteString
    , rcSender :: Async ()
    }

instance Show ReplicaConn where
    show MkReplicaConn{..} = "ReplicaConn " <> show rcSocket

data ReplicationError = ReplicaDisconnected
    deriving (Show, Exception)

type ReplicaRegistry = TVar [ReplicaConn]

initMasterState :: MasterConfig -> STM MasterState
initMasterState MkMasterConfig{..} = do
    replicaRegistry <- newTVar []
    masterReplOffset' <- newTVar 0
    pure $
        MkMasterState
            { masterReplId = "8371b4fb1155b71f4a04d3e1bc3e18c4a990aeeb"
            , masterReplOffset = masterReplOffset'
            , replicaRegistry = replicaRegistry
            , rdbConfig = rdbConfig
            }

initReplicaState :: ReplicaConfig -> STM ReplicaState
initReplicaState MkReplicaConfig{..} = do
    replicaOffset <- newTVar (-1)
    knownMasterRepl <- newTVar "?"
    pure $ MkReplicaState{..}

replicationInfo :: Replication -> STM ByteString
replicationInfo (MkReplicationMaster (MkMasterState{..})) = do
    offset <- readTVar masterReplOffset
    pure $
        BS.intercalate
            "\r\n"
            [ "role:master"
            , "master_replid:" <> masterReplId
            , "master_repl_offset:" <> fromString (show offset)
            ]
replicationInfo (MkReplicationReplica (MkReplicaState{})) = pure "role:slave"
