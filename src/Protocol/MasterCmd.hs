module Protocol.MasterCmd (
    MasterCommand (..),
    PropogationCmd (..),
    propogationCmdToCmdSTM,
    replicateIOCmdAs,
    replicateSTMCmdAs,
) where

import Data.ByteString (ByteString)
import Data.String (fromString)
import Data.Time (NominalDiffTime)

import Geo.Types (Coordinates)
import Protocol.Command
import Protocol.Result
import StoreBackend.StreamMap (XAddStreamId)

data MasterCommand
    = PropogationCmd PropogationCmd
    | ReplicationCommand MasterToReplica
    | MasterPing
    deriving (Show)

data PropogationCmd
    = RCmdLPop Key (Maybe Int)
    | RCmdRPush Key [ByteString]
    | RCmdLPush Key [ByteString]
    | RCmdSet Key ByteString (Maybe NominalDiffTime)
    | RCmdXAdd Key XAddStreamId [(ByteString, ByteString)]
    | RCmdZAdd Key ZScore ByteString
    | RCmdZRem Key ByteString
    | RCmdGeoAdd Key Coordinates ByteString
    deriving (Show)

propogationCmdToCmdSTM :: PropogationCmd -> CmdSTM
propogationCmdToCmdSTM (RCmdSet key val (Just t)) = STMString $ CmdSet key val (Just t)
propogationCmdToCmdSTM (RCmdSet key val Nothing) = STMString $ CmdSet key val Nothing
propogationCmdToCmdSTM (RCmdRPush key xs) = STMList $ CmdRPush key xs
propogationCmdToCmdSTM (RCmdLPush key xs) = STMList $ CmdLPush key xs
propogationCmdToCmdSTM (RCmdLPop key l) = STMList $ CmdLPop key l
propogationCmdToCmdSTM (RCmdXAdd key sid kvs) = STMStream $ CmdXAdd key sid kvs
propogationCmdToCmdSTM (RCmdZAdd key score val) = STMSortedSet $ CmdZAdd key score val
propogationCmdToCmdSTM (RCmdZRem key val) = STMSortedSet $ CmdZRem key val
propogationCmdToCmdSTM (RCmdGeoAdd key coords val) = STMGeo $ CmdGeoAdd key coords val

replicateIOCmdAs :: CmdIO -> Success -> Maybe PropogationCmd
replicateIOCmdAs (CmdBLPop k 0) (ReplyStrings _) = Just $ RCmdLPop k (Just 1)
replicateIOCmdAs (CmdBLPop _ 0) _ = error "Incorrect combination"
replicateIOCmdAs (CmdBLPop _ _) (ReplyResp RespArrayNull) = Nothing
replicateIOCmdAs (CmdBLPop k _) (ReplyStrings _) = Just $ RCmdLPop k (Just 1)
replicateIOCmdAs (CmdBLPop{}) _ = error "Incorrect combination"
replicateIOCmdAs (CmdXReadBlock{}) _ = Nothing

replicateSTMCmdAs :: CmdSTM -> Success -> Maybe PropogationCmd
replicateSTMCmdAs (STMString c) cr = replicateStringCmd c cr
replicateSTMCmdAs (STMList c) cr = replicateListCmd c cr
replicateSTMCmdAs (STMStream c) cr = replicateStreamCmd c cr
replicateSTMCmdAs (STMSortedSet c) cr = replicateSortedSetCmd c cr
replicateSTMCmdAs (STMGeo c) cr = replicateGeoCmd c cr
replicateSTMCmdAs CmdPing _ = Nothing
replicateSTMCmdAs (CmdEcho{}) _ = Nothing
replicateSTMCmdAs (CmdType{}) _ = Nothing
replicateSTMCmdAs CmdKeys _ = Nothing

replicateGeoCmd :: GeoCmd -> Success -> Maybe PropogationCmd
replicateGeoCmd (CmdGeoAdd k coords val) _ = Just $ RCmdGeoAdd k coords val
replicateGeoCmd (CmdGeoPos{}) _ = Nothing
replicateGeoCmd (CmdGeoDist{}) _ = Nothing
replicateGeoCmd (CmdGeoSearchByLonLatByRadius{}) _ = Nothing

replicateSortedSetCmd :: SortedSetCmd -> Success -> Maybe PropogationCmd
replicateSortedSetCmd (CmdZAdd k score v) _ = Just $ RCmdZAdd k score v
replicateSortedSetCmd (CmdZRank{}) _ = Nothing
replicateSortedSetCmd (CmdZRange{}) _ = Nothing
replicateSortedSetCmd (CmdZCard{}) _ = Nothing
replicateSortedSetCmd (CmdZScore{}) _ = Nothing
replicateSortedSetCmd (CmdZRem k v) _ = Just $ RCmdZRem k v

replicateStreamCmd :: StreamCmd -> Success -> Maybe PropogationCmd
replicateStreamCmd (CmdXAdd k sid kvs) _ = Just $ RCmdXAdd k sid kvs
replicateStreamCmd (CmdXRead{}) _ = Nothing
replicateStreamCmd (CmdXRange{}) _ = Nothing

replicateListCmd :: ListCmd -> Success -> Maybe PropogationCmd
replicateListCmd (CmdRPush k vs) _ = Just $ RCmdRPush k vs
replicateListCmd (CmdLPush k vs) _ = Just $ RCmdLPush k vs
replicateListCmd (CmdLPop k mcount) _ = Just $ RCmdLPop k mcount
replicateListCmd (CmdLRange{}) _ = Nothing
replicateListCmd (CmdLLen{}) _ = Nothing

replicateStringCmd :: StringCmd -> Success -> Maybe PropogationCmd
replicateStringCmd (CmdSet k v t) _ = Just $ RCmdSet k v t
replicateStringCmd (CmdGet{}) _ = Nothing
replicateStringCmd (CmdIncr k) (ReplyResp (RespInt v)) = Just $ RCmdSet k (fromString $ show v) Nothing
replicateStringCmd c@(CmdIncr _) cr = error $ "Incorrect combination: Command - " <> show c <> ", result - " <> show cr
