{-# LANGUAGE OverloadedStrings #-}

module Wire.Client.Command (respToCmd, cmdToResp, cmdToPretty) where

import Data.String (IsString (fromString))
import Data.Time (nominalDiffTimeToSeconds, secondsToNominalDiffTime)

import Data.ByteString (ByteString)
import Parsers (readFloatBS, readIntBS)
import Protocol.Command
import Resp.Core (Resp (..))
import Resp.Utils
import Store.StreamStoreParsing (
    readConcreteStreamId,
    readXAddStreamId,
    readXRange,
    readXReadStreamId,
    showStreamId,
    showXRange,
    showXReadStreamId,
    showXaddId,
 )
import StoreBackend.ListMap (Range (..))
import Time (millisToNominalDiffTime, nominalDiffTimeToMillis)

-- Conversion (from Resp)
respToCmd :: Resp -> Either String Command
respToCmd (Array 1 [BulkStr "PING"]) = pure $ RedSTM CmdPing
respToCmd (Array 2 [BulkStr "ECHO", BulkStr xs]) = pure $ RedSTM $ CmdEcho xs
-- Type Index
respToCmd (Array 2 [BulkStr "TYPE", BulkStr key]) = pure $ RedSTM $ CmdType key
respToCmd (Array 2 [BulkStr "KEYS", BulkStr "*"]) = pure $ RedSTM CmdKeys
-- String Store
respToCmd (Array 5 [BulkStr "SET", BulkStr key, BulkStr val, BulkStr "PX", BulkStr t]) = do
    expiry <- readIntBS t
    let expiry' = millisToNominalDiffTime expiry
    pure $ RedSTM $ STMString $ CmdSet key val (Just expiry')
respToCmd (Array 3 [BulkStr "SET", BulkStr key, BulkStr val]) =
    pure $ RedSTM $ STMString $ CmdSet key val Nothing
respToCmd (Array 2 [BulkStr "GET", BulkStr key]) = pure $ RedSTM $ STMString $ CmdGet key
respToCmd (Array 2 [BulkStr "INCR", BulkStr key]) = pure $ RedSTM $ STMString $ CmdIncr key
-- List Store
respToCmd (Array 2 [BulkStr "LLEN", BulkStr key]) = pure $ RedSTM $ STMList $ CmdLLen key
respToCmd (Array 2 [BulkStr "LPOP", BulkStr key]) = pure $ RedSTM $ STMList $ CmdLPop key Nothing
respToCmd (Array 3 [BulkStr "LPOP", BulkStr key, BulkStr len]) =
    RedSTM . STMList . CmdLPop key . Just <$> readIntBS len
respToCmd (Array 4 [BulkStr "LRANGE", BulkStr key, BulkStr st, BulkStr stop]) = do
    start <- readIntBS st
    end <- readIntBS stop
    pure $ RedSTM $ STMList $ CmdLRange key (MkRange{..})
respToCmd (Array 3 [BulkStr "BLPOP", BulkStr key, BulkStr tout]) =
    RedIO . CmdBLPop key . secondsToNominalDiffTime . realToFrac <$> readFloatBS tout
respToCmd (Array _ ((BulkStr "RPUSH") : (BulkStr key) : vals)) =
    RedSTM . STMList . CmdRPush key <$> mapM extractBulk vals
respToCmd (Array _ ((BulkStr "LPUSH") : (BulkStr key) : vals)) =
    RedSTM . STMList . CmdLPush key <$> mapM extractBulk vals
-- Stream Store
respToCmd (Array _ ((BulkStr "XADD") : (BulkStr key) : (BulkStr sId) : vals)) = do
    vals' <- mapM extractBulk vals
    chunked <- chunksOf2 vals'
    sId' <- readXAddStreamId sId
    pure $ RedSTM $ STMStream $ CmdXAdd key sId' chunked
respToCmd (Array 4 [BulkStr "XRANGE", BulkStr key, BulkStr s, BulkStr e]) =
    RedSTM . STMStream . CmdXRange key <$> readXRange s e
respToCmd (Array _ (BulkStr "XREAD" : BulkStr "streams" : vals)) = do
    vals' <- mapM extractBulk vals
    let (keys, ids) = splitAt (length vals' `div` 2) vals'
    ids' <- mapM readConcreteStreamId ids
    pure $ RedSTM $ STMStream $ CmdXRead $ zip keys ids'
respToCmd
    ( Array
            6
            [BulkStr "XREAD", BulkStr "block", BulkStr t, BulkStr "streams", BulkStr key, BulkStr sid]
        ) = do
        t' <- millisToNominalDiffTime <$> readIntBS t
        sid' <- readXReadStreamId sid
        pure $ RedIO $ CmdXReadBlock key sid' t'
-- Transaactions
respToCmd (Array 1 [BulkStr "MULTI"]) = pure $ RedTrans Multi
respToCmd (Array 1 [BulkStr "EXEC"]) = pure $ RedTrans Exec
respToCmd (Array 1 [BulkStr "DISCARD"]) = pure $ RedTrans Discard
-- Replication
respToCmd (Array 2 [BulkStr "INFO", BulkStr "replication"]) = pure $ RedInfo (Just IReplication)
respToCmd (Array 1 [BulkStr "INFO"]) = pure $ RedInfo Nothing
respToCmd (Array 3 [BulkStr "REPLCONF", BulkStr "listening-port", BulkStr port]) = RedRepl . CmdReplicaToMaster . CmdReplConfListen <$> readIntBS port
respToCmd (Array 3 [BulkStr "REPLCONF", BulkStr "capa", BulkStr "psync2"]) = pure $ RedRepl $ CmdReplicaToMaster CmdReplConfCapabilities
respToCmd (Array 3 [BulkStr "REPLCONF", BulkStr "GETACK", BulkStr "*"]) = pure $ RedRepl $ CmdMasterToReplica CmdReplConfGetAck
respToCmd (Array 3 [BulkStr "REPLCONF", BulkStr "ACK", BulkStr n]) = RedRepl . CmdReplicaToMaster . CmdReplConfAck <$> readIntBS n
respToCmd (Array 3 [BulkStr "PSYNC", BulkStr sid, BulkStr offset]) = do
    offset' <- readIntBS offset
    pure $ RedRepl $ CmdReplicaToMaster $ CmdPSync sid offset'
respToCmd (Array 3 [BulkStr "WAIT", BulkStr n, BulkStr t]) = do
    n' <- readIntBS n
    t' <- millisToNominalDiffTime <$> readIntBS t
    pure $ CmdWait n' t'
-- CONFIG
respToCmd (Array 3 [BulkStr "CONFIG", BulkStr "GET", BulkStr "dir"]) = pure $ RedConfig ConfigDir
respToCmd (Array 3 [BulkStr "CONFIG", BulkStr "GET", BulkStr "dbfilename"]) = pure $ RedConfig ConfigDbFilename
-- PubSub
respToCmd (Array 2 [BulkStr "SUBSCRIBE", BulkStr channel]) = pure $ RedSub $ CmdSubscribe channel
respToCmd (Array 3 [BulkStr "PUBLISH", BulkStr channel, BulkStr msg]) = pure $ RedSub $ CmdPublish channel msg
-- Unhandled
respToCmd r = Left $ "Conversion Error" <> show r

-- Conversion (to Resp)

cmdToResp :: Command -> Resp
cmdToResp (RedSTM cmd) = stmCmdToResp cmd
cmdToResp (RedTrans cmd) = txCmdToResp cmd
cmdToResp (RedRepl cmd) = replicationCmdToResp cmd
cmdToResp (RedIO cmd) = ioCmdToResp cmd
cmdToResp (RedInfo cmd) = infoCmdToResp cmd
cmdToResp (RedConfig cmd) = configCmdToResp cmd
cmdToResp (RedSub cmd) = subCmdToResp cmd
cmdToResp (CmdWait n t) = Array 3 [BulkStr "WAIT", BulkStr $ fromString $ show n, BulkStr $ fromString $ show $ nominalDiffTimeToMillis t]

subCmdToResp :: PubSub -> Resp
subCmdToResp (CmdSubscribe chan) = Array 2 [BulkStr "SUBSCRIBE", BulkStr chan]
subCmdToResp (CmdPublish chan msg) = Array 3 [BulkStr "PUBLISH", BulkStr chan, BulkStr msg]

configCmdToResp :: SubConfig -> Resp
configCmdToResp ConfigDir = Array 3 [BulkStr "CONFIG", BulkStr "GET", BulkStr "dir"]
configCmdToResp ConfigDbFilename = Array 3 [BulkStr "CONFIG", BulkStr "GET", BulkStr "dbfilename"]

ioCmdToResp :: CmdIO -> Resp
ioCmdToResp (CmdBLPop key t) = Array 3 [BulkStr "BLPOP", BulkStr key, BulkStr $ fromString $ show $ nominalDiffTimeToSeconds t]
ioCmdToResp (CmdXReadBlock key sid t) = Array 6 [BulkStr "XREAD", BulkStr "block", BulkStr $ fromString $ show $ nominalDiffTimeToMillis t, BulkStr "streams", BulkStr key, BulkStr $ showXReadStreamId sid]

replicationCmdToResp :: CmdReplication -> Resp
replicationCmdToResp (CmdReplicaToMaster (CmdReplConfListen port)) = Array 3 [BulkStr "REPLCONF", BulkStr "listening-port", BulkStr $ fromString $ show port]
replicationCmdToResp (CmdReplicaToMaster CmdReplConfCapabilities) = Array 3 [BulkStr "REPLCONF", BulkStr "capa", BulkStr "psync2"]
replicationCmdToResp (CmdReplicaToMaster (CmdPSync sId s)) = Array 3 [BulkStr "PSYNC", BulkStr sId, BulkStr $ fromString $ show s]
replicationCmdToResp (CmdReplicaToMaster (CmdReplConfAck n)) = Array 3 [BulkStr "REPLCONF", BulkStr "ACK", BulkStr $ fromString $ show n]
replicationCmdToResp (CmdMasterToReplica CmdReplConfGetAck) = Array 3 [BulkStr "REPLCONF", BulkStr "GETACK", BulkStr "*"]

stmCmdToResp :: CmdSTM -> Resp
stmCmdToResp CmdPing = Array 1 [BulkStr "PING"]
stmCmdToResp (CmdEcho xs) = Array 2 [BulkStr "ECHO", BulkStr xs]
stmCmdToResp (CmdType key) = Array 2 [BulkStr "TYPE", BulkStr key]
stmCmdToResp (STMString cmd) = stringStoreCmdToResp cmd
stmCmdToResp (STMList cmd) = listStmCmdToResp cmd
stmCmdToResp (STMStream cmd) = streamStmCmdToResp cmd
stmCmdToResp CmdKeys = Array 2 [BulkStr "KEYS", BulkStr "*"]

txCmdToResp :: CmdTransaction -> Resp
txCmdToResp Multi = Array 1 [BulkStr "MULTI"]
txCmdToResp Exec = Array 1 [BulkStr "EXEC"]
txCmdToResp Discard = Array 1 [BulkStr "DISCARD"]

stringStoreCmdToResp :: StringCmd -> Resp
stringStoreCmdToResp (CmdSet key val (Just t)) = Array 5 [BulkStr "SET", BulkStr key, BulkStr val, BulkStr "PX", BulkStr $ fromString $ show $ nominalDiffTimeToMillis t]
stringStoreCmdToResp (CmdSet key val Nothing) = Array 3 [BulkStr "SET", BulkStr key, BulkStr val]
stringStoreCmdToResp (CmdIncr key) = Array 2 [BulkStr "INCR", BulkStr key]
stringStoreCmdToResp (CmdGet key) = Array 2 [BulkStr "GET", BulkStr key]

listStmCmdToResp :: ListCmd -> Resp
listStmCmdToResp (CmdRPush key xs) = Array (length xs + 2) (BulkStr "RPUSH" : BulkStr key : map BulkStr xs)
listStmCmdToResp (CmdLPush key xs) = Array (length xs + 2) (BulkStr "LPUSH" : BulkStr key : map BulkStr xs)
listStmCmdToResp (CmdLPop key Nothing) = Array 2 [BulkStr "LPOP", BulkStr key]
listStmCmdToResp (CmdLPop key (Just len)) = Array 3 [BulkStr "LPOP", BulkStr key, BulkStr $ fromString $ show len]
listStmCmdToResp (CmdLLen key) = Array 2 [BulkStr "LLEN", BulkStr key]
listStmCmdToResp (CmdLRange key (MkRange start stop)) = Array 4 [BulkStr "LRANGE", BulkStr key, BulkStr $ fromString $ show start, BulkStr $ fromString $ show stop]

streamStmCmdToResp :: StreamCmd -> Resp
streamStmCmdToResp (CmdXAdd key sid kvs) =
    Array (3 + length kvs * 2) $ BulkStr "XADD" : BulkStr key : BulkStr (showXaddId sid) : concatMap (\(k, v) -> [BulkStr k, BulkStr v]) kvs
streamStmCmdToResp (CmdXRange key xr) =
    Array 4 $ BulkStr "XRANGE" : BulkStr key : map BulkStr [s, e]
  where
    (s, e) = showXRange xr
streamStmCmdToResp (CmdXRead xs) =
    Array (2 + length keys + length ids) $ BulkStr "XREAD" : BulkStr "streams" : map BulkStr keys ++ map (BulkStr . showStreamId) ids
  where
    (keys, ids) = unzip xs

infoCmdToResp :: Maybe SubInfo -> Resp
infoCmdToResp (Just IReplication) = Array 2 [BulkStr "INFO", BulkStr "replication"]
infoCmdToResp Nothing = Array 1 [BulkStr "INFO"]

-- command pretty names

cmdToPretty :: Command -> ByteString
cmdToPretty (RedSTM cmd) = stmCmdToPretty cmd
cmdToPretty (RedTrans cmd) = txCmdToPretty cmd
cmdToPretty (RedRepl cmd) = replicationCmdToPretty cmd
cmdToPretty (RedIO cmd) = ioCmdToPretty cmd
cmdToPretty (RedInfo cmd) = infoCmdToPretty cmd
cmdToPretty (RedConfig cmd) = configCmdToPretty cmd
cmdToPretty (RedSub cmd) = subCmdToPretty cmd
cmdToPretty (CmdWait{}) = "WAIT"

subCmdToPretty :: PubSub -> ByteString
subCmdToPretty (CmdSubscribe{}) = "SUBSCRIBE"
subCmdToPretty (CmdPublish{}) = "PUBLISH"

configCmdToPretty :: SubConfig -> ByteString
configCmdToPretty ConfigDir = "CONFIG"
configCmdToPretty ConfigDbFilename = "CONFIG"

ioCmdToPretty :: CmdIO -> ByteString
ioCmdToPretty (CmdBLPop{}) = "BLPOP"
ioCmdToPretty (CmdXReadBlock{}) = "XREAD"

replicationCmdToPretty :: CmdReplication -> ByteString
replicationCmdToPretty (CmdReplicaToMaster (CmdReplConfListen{})) = "REPLCONF"
replicationCmdToPretty (CmdReplicaToMaster CmdReplConfCapabilities) = "REPLCONF"
replicationCmdToPretty (CmdReplicaToMaster (CmdPSync{})) = "PSYNC"
replicationCmdToPretty (CmdReplicaToMaster (CmdReplConfAck{})) = "REPLCONF"
replicationCmdToPretty (CmdMasterToReplica CmdReplConfGetAck) = "REPLCONF"

stmCmdToPretty :: CmdSTM -> ByteString
stmCmdToPretty CmdPing = "PING"
stmCmdToPretty (CmdEcho{}) = "ECHO"
stmCmdToPretty (CmdType{}) = "TYPE"
stmCmdToPretty (STMString cmd) = stringStoreCmdToPretty cmd
stmCmdToPretty (STMList cmd) = listStmCmdToPretty cmd
stmCmdToPretty (STMStream cmd) = streamStmCmdToPretty cmd
stmCmdToPretty CmdKeys = "KEYS"

txCmdToPretty :: CmdTransaction -> ByteString
txCmdToPretty Multi = "MULTI"
txCmdToPretty Exec = "EXEC"
txCmdToPretty Discard = "DISCARD"

stringStoreCmdToPretty :: StringCmd -> ByteString
stringStoreCmdToPretty (CmdSet{}) = "SET"
stringStoreCmdToPretty (CmdIncr{}) = "INCR"
stringStoreCmdToPretty (CmdGet{}) = "GET"

listStmCmdToPretty :: ListCmd -> ByteString
listStmCmdToPretty (CmdRPush{}) = "RPUSH"
listStmCmdToPretty (CmdLPush{}) = "LPUSH"
listStmCmdToPretty (CmdLPop{}) = "LPOP"
listStmCmdToPretty (CmdLLen{}) = "LLEN"
listStmCmdToPretty (CmdLRange{}) = "LRANGE"

streamStmCmdToPretty :: StreamCmd -> ByteString
streamStmCmdToPretty (CmdXAdd{}) = "XADD"
streamStmCmdToPretty (CmdXRange{}) = "XRANGE"
streamStmCmdToPretty (CmdXRead{}) = "XREAD"

infoCmdToPretty :: Maybe SubInfo -> ByteString
infoCmdToPretty (Just IReplication) = "INFO"
infoCmdToPretty Nothing = "INFO"
