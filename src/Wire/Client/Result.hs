{-# LANGUAGE OverloadedStrings #-}

module Wire.Client.Result (cmdResultToResp, respToResult, errorToResp, resultToResp) where

import Data.ByteString (ByteString)
import Data.ByteString qualified as BS
import Data.String (IsString (fromString))
import Text.Parsec (anyChar, char, manyTill, string)
import Text.Parsec.ByteString (Parser)

import Geo.Types (Coordinates (..))
import Parsers (intParser, parseBS, readIntBS)
import Protocol.Result
import Resp.Core (Resp (..))
import StoreBackend.StreamMap (ConcreteStreamId, StreamMapError (..))
import StoreBackend.StreamMap qualified as SM
import StoreBackend.TypeIndex (ValueType (..))
import Wire.Client.Command (cmdToPretty)

resultToResp :: Result -> Resp
resultToResp (ResNormal r) = cmdResultToResp r
resultToResp (ResError e) = errorToResp e
resultToResp (ResCombined er) = Array (length er) (map (either errorToResp cmdResultToResp) er)
resultToResp ResNothing = error "Nothing can't be converted to resp"

cmdResultToResp :: CommandResult -> Resp
cmdResultToResp ResPong = Str "PONG"
cmdResultToResp ResPongSubscribed = Array 2 [BulkStr "pong", BulkStr ""]
cmdResultToResp ResOk = Str "OK"
cmdResultToResp (ResType Nothing) = Str "none"
cmdResultToResp (ResType (Just t)) = valueTypeToResp t
cmdResultToResp (RSimple s) = Str s
cmdResultToResp (RBulk Nothing) = NullBulk
cmdResultToResp (RBulk (Just b)) = BulkStr b
cmdResultToResp (RInt n) = Int n
cmdResultToResp (RIntOrNil Nothing) = NullBulk
cmdResultToResp (RIntOrNil (Just n)) = Int n
cmdResultToResp (RDoubleOrNil Nothing) = NullBulk
cmdResultToResp (RDoubleOrNil (Just n)) = BulkStr $ fromString $ show n
cmdResultToResp RArrayNull = NullArray
cmdResultToResp (RArraySimple xs) = arrayMap BulkStr xs
cmdResultToResp (RArrayStreamValues vals) = arrayMap valueToResp vals
cmdResultToResp (RArrayKeyValues kvs) = arrayMap arrayKeyValsToResp kvs
cmdResultToResp (RStreamId sid) = streamIdToResp sid
cmdResultToResp (ResSubscribed chan n) = Array 3 [BulkStr "subscribe", BulkStr chan, Int n]
cmdResultToResp (ResUnsubscribed chan n) = Array 3 [BulkStr "unsubscribe", BulkStr chan, Int n]
cmdResultToResp (RCoordinates coords) = Array (length coords) (map (maybe NullArray coordsToRes) coords)
cmdResultToResp (RRepl r) = replResultToResp r

coordsToRes :: Coordinates -> Resp
coordsToRes (MkCoordinates lat long) = Array 2 [BulkStr $ fromString $ show long, BulkStr $ fromString $ show lat]

replResultToResp :: ReplResult -> Resp
replResultToResp ReplOk = Str "OK"
replResultToResp (ReplConfAck n) = Array 3 [BulkStr "REPLCONF", BulkStr "ACK", BulkStr $ fromString $ show n]
replResultToResp (ResFullResync sId s) = Str $ BS.intercalate " " ["FULLRESYNC", sId, fromString $ show s]

valueTypeToResp :: ValueType -> Resp
valueTypeToResp VString = Str "string"
valueTypeToResp VList = Str "list"
valueTypeToResp VStream = Str "stream"
valueTypeToResp VChannel = Str "channel"
valueTypeToResp VSortedSet = Str "sortedset"

arrayMap :: (a -> Resp) -> [a] -> Resp
arrayMap f xs = Array (length xs) (map f xs)

streamIdToResp :: ConcreteStreamId -> Resp
streamIdToResp (ms, i) = BulkStr . fromString $ show ms <> "-" <> show i

valueToResp :: SM.Value ByteString ByteString -> Resp
valueToResp v = Array 2 [streamIdToResp v.streamId, arrayMap BulkStr $ vals v.vals]
  where
    vals :: [(ByteString, ByteString)] -> [ByteString]
    vals = concatMap (\(k, v') -> [k, v'])

arrayKeyValsToResp :: (ByteString, [SM.Value ByteString ByteString]) -> Resp
arrayKeyValsToResp (k, vs) = Array 2 [BulkStr k, arrayMap valueToResp vs]

----

respToResult :: Resp -> Either String Result
respToResult (Str "PONG") = Right $ ResNormal ResPong
respToResult (Str "OK") = Right $ ResNormal ResOk
respToResult (Str s)
    | "FULLRESYNC " `BS.isPrefixOf` s = ResNormal . RRepl <$> parseBS fullresyncParser s
    | otherwise = Right $ ResNormal $ RSimple s
respToResult (Int i) = Right $ ResNormal $ RInt i
respToResult (Array 3 [BulkStr "REPLCONF", BulkStr "ACK", BulkStr n]) = ResNormal . RRepl . ReplConfAck <$> readIntBS n
respToResult (StrErr s) = respToErr s
respToResult NullArray = Right $ ResNormal RArrayNull
respToResult NullBulk = Right $ ResNormal $ RBulk Nothing
respToResult (BulkStr s) = Right $ ResNormal $ RBulk $ Just s
respToResult r = error $ "TODO: " <> show r

fullresyncParser :: Parser ReplResult
fullresyncParser = do
    _ <- string "FULLRESYNC "
    sId <- fromString <$> manyTill anyChar (char ' ')
    ResFullResync sId <$> intParser

-- error conversions

strStreamErrBaseStreamId, strStreamErrNotLargerId, strRExecWithoutMulti, strRNotSupportedInTx, strRMultiInMulti, strRDiscardWithoutMulti, strIncrError :: ByteString
strStreamErrBaseStreamId = "ERR The ID specified in XADD must be greater than 0-0"
strStreamErrNotLargerId = "ERR The ID specified in XADD is equal or smaller than the target stream top item"
strRExecWithoutMulti = "ERR EXEC without MULTI"
strRNotSupportedInTx = "ERR command not supported in transaction"
strRMultiInMulti = "ERR MULTI inside MULTI"
strRDiscardWithoutMulti = "ERR DISCARD without MULTI"
strIncrError = "ERR value is not an integer or out of range"

errorToResp :: CommandError -> Resp
errorToResp (RStreamError e) = streamMapErrorToResp e
errorToResp RIncrError = StrErr strIncrError
errorToResp (RTxErr txErr) = txErrorToResp txErr
errorToResp (RCmdNotAllowedInMode cmd mode) = StrErr $ "ERR Can't execute '" <> cmdToPretty cmd <> "'in " <> modeToPretty mode <> "mode"
errorToResp (RInvalidLatLong (MkCoordinates lat long)) = StrErr $ "ERR invalid longitude,latitude pair " <> fromString (show long) <> "," <> fromString (show lat)

txErrorToResp :: TransactionError -> Resp
txErrorToResp RExecWithoutMulti = StrErr strRExecWithoutMulti
txErrorToResp RNotSupportedInTx = StrErr strRNotSupportedInTx
txErrorToResp RMultiInMulti = StrErr strRMultiInMulti
txErrorToResp RDiscardWithoutMulti = StrErr strRDiscardWithoutMulti

streamMapErrorToResp :: StreamMapError -> Resp
streamMapErrorToResp BaseStreamId = StrErr strStreamErrBaseStreamId
streamMapErrorToResp NotLargerId = StrErr strStreamErrNotLargerId

respToErr :: ByteString -> Either String Result
respToErr s
    | s == strStreamErrBaseStreamId = Right $ ResError $ RStreamError BaseStreamId
    | s == strStreamErrNotLargerId = Right $ ResError $ RStreamError NotLargerId
    | s == strRExecWithoutMulti = Right $ ResError $ RTxErr RExecWithoutMulti
    | s == strRNotSupportedInTx = Right $ ResError $ RTxErr RNotSupportedInTx
    | s == strRMultiInMulti = Right $ ResError $ RTxErr RMultiInMulti
    | s == strRDiscardWithoutMulti = Right $ ResError $ RTxErr RDiscardWithoutMulti
    | s == strIncrError = Right $ ResError RIncrError
    | otherwise = Left $ "unknown error: " <> show s
