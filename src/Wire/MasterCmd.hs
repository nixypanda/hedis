{-# LANGUAGE OverloadedStrings #-}

module Wire.MasterCmd (masterCmdToResp, respToMasterCmd, propogationCmdToResp) where

import Data.String (fromString)

import Parsers (readFloatBS, readIntBS)
import Protocol.Command
import Protocol.MasterCmd
import Resp.Core (Resp (..))
import Resp.Utils
import Store.StreamStoreParsing (readXAddStreamId, showXaddId)
import Time (millisToNominalDiffTime, nominalDiffTimeToMillis)

masterCmdToResp :: MasterCommand -> Resp
masterCmdToResp MasterPing = Array 1 [BulkStr "PING"]
masterCmdToResp (ReplicationCommand CmdReplConfGetAck) = Array 3 [BulkStr "REPLCONF", BulkStr "GETACK", BulkStr "*"]
masterCmdToResp (PropogationCmd cmd) = propogationCmdToResp cmd

propogationCmdToResp :: PropogationCmd -> Resp
propogationCmdToResp (RCmdSet key val (Just t)) = Array 5 [BulkStr "SET", BulkStr key, BulkStr val, BulkStr "PX", BulkStr $ fromString $ show $ nominalDiffTimeToMillis t]
propogationCmdToResp (RCmdSet key val Nothing) = Array 3 [BulkStr "SET", BulkStr key, BulkStr val]
propogationCmdToResp (RCmdRPush key xs) = Array (length xs + 2) (BulkStr "RPUSH" : BulkStr key : map BulkStr xs)
propogationCmdToResp (RCmdLPush key xs) = Array (length xs + 2) (BulkStr "LPUSH" : BulkStr key : map BulkStr xs)
propogationCmdToResp (RCmdLPop key Nothing) = Array 2 [BulkStr "LPOP", BulkStr key]
propogationCmdToResp (RCmdLPop key (Just len)) = Array 3 [BulkStr "LPOP", BulkStr key, BulkStr $ fromString $ show len]
propogationCmdToResp (RCmdXAdd key sid kvs) = Array (3 + length kvs * 2) $ BulkStr "XADD" : BulkStr key : BulkStr (showXaddId sid) : concatMap (\(k, v) -> [BulkStr k, BulkStr v]) kvs
propogationCmdToResp (RCmdZAdd key score value) = Array 4 [BulkStr "ZADD", BulkStr key, BulkStr $ fromString $ show score, BulkStr value]
propogationCmdToResp (RCmdZRem key value) = Array 3 [BulkStr "ZREM", BulkStr key, BulkStr value]

respToMasterCmd :: Resp -> Either String MasterCommand
respToMasterCmd (Array 1 [BulkStr "PING"]) = pure MasterPing
respToMasterCmd (Array 3 [BulkStr "REPLCONF", BulkStr "GETACK", BulkStr "*"]) = pure $ ReplicationCommand CmdReplConfGetAck
respToMasterCmd (Array 5 [BulkStr "SET", BulkStr key, BulkStr val, BulkStr "PX", BulkStr t]) = do
    expiry <- readIntBS t
    let expiry' = millisToNominalDiffTime expiry
    pure . PropogationCmd $ RCmdSet key val (Just expiry')
respToMasterCmd (Array 3 [BulkStr "SET", BulkStr key, BulkStr val]) =
    pure . PropogationCmd $ RCmdSet key val Nothing
respToMasterCmd (Array 2 [BulkStr "LPOP", BulkStr key]) = pure . PropogationCmd $ RCmdLPop key Nothing
respToMasterCmd (Array 3 [BulkStr "LPOP", BulkStr key, BulkStr len]) =
    PropogationCmd . RCmdLPop key . Just <$> readIntBS len
respToMasterCmd (Array _ ((BulkStr "RPUSH") : (BulkStr key) : vals)) =
    PropogationCmd . RCmdRPush key <$> mapM extractBulk vals
respToMasterCmd (Array _ ((BulkStr "LPUSH") : (BulkStr key) : vals)) =
    PropogationCmd . RCmdLPush key <$> mapM extractBulk vals
-- Stream Store
respToMasterCmd (Array _ ((BulkStr "XADD") : (BulkStr key) : (BulkStr sId) : vals)) = do
    vals' <- mapM extractBulk vals
    chunked <- chunksOf2 vals'
    sId' <- readXAddStreamId sId
    pure $ PropogationCmd $ RCmdXAdd key sId' chunked
-- Sorted Set
respToMasterCmd (Array 4 [BulkStr "ZADD", BulkStr k, BulkStr score, BulkStr v]) = do
    score' <- readFloatBS score
    pure . PropogationCmd $ RCmdZAdd k (realToFrac score') v
respToMasterCmd (Array 3 [BulkStr "ZREM", BulkStr k, BulkStr v]) = pure . PropogationCmd $ RCmdZRem k v
respToMasterCmd c = Left $ "Invalid Propogation Command: -> " <> show c
