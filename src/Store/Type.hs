{-# LANGUAGE DeriveAnyClass #-}

module Store.Type (
    TypeIndex,
    IncorrectType,
    emptySTM,
    getTypeSTM,
    getKeysSTM,
    availableSTM,
    setTypeSTM,
    setIfAvailable,
) where

import Control.Concurrent.STM (STM, TVar, modifyTVar', newTVar, readTVar, throwSTM)
import Control.Exception (Exception)
import Control.Monad (unless)
import Data.ByteString (ByteString)
import Data.Map (Map)
import Data.Map qualified as M

import Store.Backend.TypeIndex (RequiredType (..), ValueType, checkAvailable)

type Key = ByteString
type TypeIndex = Map Key ValueType

data IncorrectType = IncorrectType deriving (Exception, Show)

emptySTM :: STM (TVar TypeIndex)
emptySTM = newTVar M.empty

getTypeSTM :: TVar TypeIndex -> Key -> STM (Maybe ValueType)
getTypeSTM tvIdx key = M.lookup key <$> readTVar tvIdx

getKeysSTM :: TVar TypeIndex -> STM [Key]
getKeysSTM tvIdx = M.keys <$> readTVar tvIdx

availableSTM :: TVar TypeIndex -> Key -> RequiredType -> STM ()
availableSTM tvIdx key req = do
    idx <- readTVar tvIdx
    unless (checkAvailable (M.lookup key idx) req) $
        throwSTM IncorrectType

setTypeSTM :: TVar TypeIndex -> Key -> ValueType -> STM ()
setTypeSTM tvIdx key ty =
    modifyTVar' tvIdx (M.insert key ty)

setIfAvailable :: TVar TypeIndex -> Key -> ValueType -> STM ()
setIfAvailable tvIdx key ty = availableSTM tvIdx key (AbsentOr ty) *> setTypeSTM tvIdx key ty
