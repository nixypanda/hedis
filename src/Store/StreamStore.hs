module Store.StreamStore (
    StreamStore,
    emptySTM,
    xResolveStreamIdSTM,
    xReadBlockSTM,
    xAddSTM,
    xRangeSTM,
    xReadSTM,
) where

import Control.Concurrent.STM (STM, TVar, newTVar, readTVar, retry, writeTVar)
import Data.ByteString (ByteString)
import Data.Time (UTCTime)

import StoreBackend.StreamMap (
    ConcreteStreamId,
    StreamMap,
    StreamMapError,
    XAddStreamId,
    XReadStreamId (..),
 )
import StoreBackend.StreamMap qualified as SM

type Key = ByteString
type StreamStore = StreamMap Key ByteString ByteString
type SKey = ByteString
type SVal = ByteString

emptySTM :: STM (TVar StreamStore)
emptySTM = newTVar SM.empty

xAddSTM :: TVar StreamStore -> Key -> XAddStreamId -> [(SKey, SVal)] -> UTCTime -> STM (Either StreamMapError ConcreteStreamId)
xAddSTM tv key sid vals time = do
    m <- readTVar tv
    case SM.insert key sid vals time m of
        Left err -> pure $ Left err
        Right (csid, m') -> do
            writeTVar tv m'
            pure $ Right csid

xRangeSTM :: TVar StreamStore -> Key -> SM.XRange -> STM [SM.Value ByteString ByteString]
xRangeSTM tv key range = do
    m <- readTVar tv
    pure (SM.query key range m)

xReadSTM ::
    TVar StreamStore ->
    [(Key, ConcreteStreamId)] ->
    STM [(Key, [SM.Value ByteString ByteString])]
xReadSTM tv keys = do
    m <- readTVar tv
    pure $ map (\(k, sid) -> (k, SM.queryEx k sid m)) keys

xResolveStreamIdSTM :: TVar StreamStore -> Key -> XReadStreamId -> STM ConcreteStreamId
xResolveStreamIdSTM tv key sid = do
    m <- readTVar tv
    case sid of
        Dollar -> pure $ SM.lookupLatest key m
        Concrete s -> pure s

xReadBlockSTM :: TVar StreamStore -> Key -> ConcreteStreamId -> STM (Key, [SM.Value SKey SVal])
xReadBlockSTM tv key sid = do
    mInner <- readTVar tv
    case SM.queryEx key sid mInner of
        [] -> retry
        xs -> pure (key, xs)
