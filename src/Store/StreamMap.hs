module Store.StreamMap (
    ConcreteStreamId,
    XAddStreamId (..),
    IdSeq (..),
    StreamMap,
    StreamMapError (..),
    Value (..),
    XRange (..),
    XRangeStart (..),
    XRangeEnd (..),
    XRangeStreamId,
    XReadStreamId (..),
    (!),
    empty,
    fromList,
    insert,
    query,
    queryEx,
    lookupLatest,
) where

import Data.List (unsnoc)
import Data.Map (Map)
import Data.Map qualified as M
import Data.Maybe (fromJust, fromMaybe)
import Data.Time (UTCTime)

import Time (utcToMillis)

-- Stream Id

type ConcreteStreamId = (Int, Int)

baseStreamId :: ConcreteStreamId
baseStreamId = (0, 0)

endStreamId :: ConcreteStreamId
endStreamId = (maxBound, maxBound)

baseStreamIdWIthSeqStart :: Int -> ConcreteStreamId
baseStreamIdWIthSeqStart ts = (ts, 0)

baseStreamIdWithSeqEnd :: Int -> ConcreteStreamId
baseStreamIdWithSeqEnd ts = (ts, maxBound)

-- XAdd StreamId

data XAddStreamId = AutoId | ExplicitId Int IdSeq
    deriving (Show, Eq)

data IdSeq = Seq Int | SeqAuto
    deriving (Show, Eq)

data Value k v = MkValue
    { streamId :: !ConcreteStreamId
    , vals :: ![(k, v)]
    }
    deriving (Eq, Ord, Show)

-- XRead Stream Id

data XReadStreamId = Dollar | Concrete ConcreteStreamId
    deriving (Eq, Ord, Show)

-- Stream Error

data StreamMapError = BaseStreamId | NotLargerId
    deriving (Show, Eq)

-- XRange StreamId

type XRangeStreamId = (Int, Maybe Int)

data XRangeStart = XS XRangeStreamId | XMinus deriving (Show, Eq)
data XRangeEnd = XE XRangeStreamId | XPlus deriving (Show, Eq)

data XRange = MkXrange {start :: !XRangeStart, end :: !XRangeEnd}
    deriving (Show, Eq)

xRangeToConcrete :: XRange -> (ConcreteStreamId, ConcreteStreamId)
xRangeToConcrete (MkXrange{..}) = (xRangeStartToConcrete start, xRangeEndToConcrete end)
  where
    xRangeEndToConcrete :: XRangeEnd -> ConcreteStreamId
    xRangeEndToConcrete XPlus = endStreamId
    xRangeEndToConcrete (XE (ts, Just ms)) = (ts, ms)
    xRangeEndToConcrete (XE (ts, Nothing)) = baseStreamIdWithSeqEnd ts

    xRangeStartToConcrete :: XRangeStart -> ConcreteStreamId
    xRangeStartToConcrete XMinus = baseStreamId
    xRangeStartToConcrete (XS (ts, Just ms)) = (ts, ms)
    xRangeStartToConcrete (XS (ts, Nothing)) = baseStreamIdWIthSeqStart ts

-- Stream Map

type StreamMap k ik v = Map k [Value ik v]

empty :: StreamMap k ik a
empty = M.empty

fromList :: (Ord k) => [(k, a)] -> Map k a
fromList = M.fromList

(!) :: (Ord k) => Map k a -> k -> a
m ! i = fromJust $ M.lookup i m

insert :: (Ord k) => k -> XAddStreamId -> [(ik, v)] -> UTCTime -> StreamMap k ik v -> Either StreamMapError (ConcreteStreamId, StreamMap k ik v)
insert key sid values time sMap
    | streamId == baseStreamId = Left BaseStreamId
    | Nothing <- mxs = okInsert [] streamId
    | Just [] <- mxs = okInsert [] streamId
    | Just xs <- mxs, Just (_, lastVal) <- unsnoc xs, lastVal.streamId >= streamId = Left NotLargerId
    | Just xs <- mxs = okInsert xs streamId
  where
    mxs = M.lookup key sMap
    oldSeq :: (Int, Int)
    oldSeq =
        case mxs >>= unsnoc of
            Just (_, v) -> v.streamId
            Nothing -> (0, 0)

    streamId :: ConcreteStreamId
    streamId =
        case sid of
            AutoId -> let ts = utcToMillis time in if ts == fst oldSeq then (ts, snd oldSeq + 1) else (ts, 0)
            ExplicitId ts (Seq i) -> (ts, i)
            ExplicitId ts SeqAuto -> if ts == fst oldSeq then (ts, snd oldSeq + 1) else (ts, 0)

    okInsert xs sid' =
        Right
            ( sid'
            , M.insert key (xs ++ [MkValue sid' values]) sMap
            )

query :: (Ord k) => k -> XRange -> StreamMap k ik v -> [Value ik v]
query key range sMap = takeWhile (\elm -> elm.streamId <= e) $ dropWhile (\elm -> elm.streamId < s) vals
  where
    (s, e) = xRangeToConcrete range
    vals = fromMaybe [] $ M.lookup key sMap

queryEx :: (Ord k) => k -> ConcreteStreamId -> StreamMap k ik v -> [Value ik v]
queryEx key streamId sMap = dropWhile (\elm -> elm.streamId <= streamId) $ fromMaybe [] $ M.lookup key sMap

lookupLatest :: (Ord k) => k -> StreamMap k ik v -> ConcreteStreamId
lookupLatest k sMap = case M.lookup k sMap of
    Nothing -> baseStreamId
    Just xs -> case unsnoc xs of
        Nothing -> baseStreamId
        Just (_, x) -> x.streamId
