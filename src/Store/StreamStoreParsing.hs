{-# LANGUAGE OverloadedStrings #-}

module Store.StreamStoreParsing (
    readXAddStreamId,
    readXRange,
    readXReadStreamId,
    readConcreteStreamId,
    showXRange,
    showXaddId,
    showStreamId,
    showXReadStreamId,
) where

import Control.Applicative ((<|>))
import Control.Monad (join)
import Data.ByteString (ByteString)
import Data.Functor (($>))
import Data.String (fromString)
import Text.Parsec (char, eof, optionMaybe, spaces)
import Text.Parsec.ByteString (Parser)

import Parsers (intParser, parseBS)
import Store.Backend.StreamMap (
    ConcreteStreamId,
    IdSeq (..),
    XAddStreamId (..),
    XRange (..),
    XRangeEnd (..),
    XRangeStart (..),
    XRangeStreamId,
    XReadStreamId (..),
 )

-- Redis stream id

xAddStreamIdParser :: Parser XAddStreamId
xAddStreamIdParser = autoId <|> explicitId

autoId :: Parser XAddStreamId
autoId = char '*' $> AutoId

explicitId :: Parser XAddStreamId
explicitId = do
    ts <- intParser
    _ <- char '-'
    ExplicitId ts <$> seqParser

seqParser :: Parser IdSeq
seqParser = (char '*' $> SeqAuto) <|> (Seq <$> intParser)

readXAddStreamId :: ByteString -> Either String XAddStreamId
readXAddStreamId = parseBS xAddStreamIdParser

-- Xrange

xRangeStreamId :: Parser XRangeStreamId
xRangeStreamId = do
    ts <- intParser
    mSeq <- optionMaybe (char '-' *> optionMaybe intParser)
    pure (ts, join mSeq)

xRangeStartParser :: Parser XRangeStart
xRangeStartParser = (char '-' $> XMinus) <|> (XS <$> xRangeStreamId)

xRangeEndParser :: Parser XRangeEnd
xRangeEndParser = (char '+' $> XPlus) <|> (XE <$> xRangeStreamId)

xRangeParser :: Parser XRange
xRangeParser = do
    s <- xRangeStartParser
    spaces
    e <- xRangeEndParser
    eof
    pure (MkXrange s e)

readXRange :: ByteString -> ByteString -> Either String XRange
readXRange s e = parseBS xRangeParser (s <> " " <> e)

-- Concreate StreamId

concreteStreamIdParser :: Parser ConcreteStreamId
concreteStreamIdParser = do
    ts <- intParser
    _ <- char '-'
    (,) ts <$> intParser

xReadStreamIdParser :: Parser XReadStreamId
xReadStreamIdParser = char '$' $> Dollar <|> (Concrete <$> concreteStreamIdParser)

readConcreteStreamId :: ByteString -> Either String ConcreteStreamId
readConcreteStreamId = parseBS concreteStreamIdParser

readXReadStreamId :: ByteString -> Either String XReadStreamId
readXReadStreamId = parseBS xReadStreamIdParser

--- TO string

showXaddId :: XAddStreamId -> ByteString
showXaddId AutoId = "*"
showXaddId (ExplicitId i SeqAuto) = fromString (show i) <> "-*"
showXaddId (ExplicitId i (Seq s)) = fromString (show i) <> "-" <> fromString (show s)

showXRange :: XRange -> (ByteString, ByteString)
showXRange (MkXrange start end) = (xRangeStartToStr start, xRangeEndToStr end)
  where
    xRangeEndToStr XPlus = "+"
    xRangeEndToStr (XE sid) = xRangeStreamIdToStr sid

    xRangeStartToStr XMinus = "-"
    xRangeStartToStr (XS sid) = xRangeStreamIdToStr sid

    xRangeStreamIdToStr (ts, Just ms) = fromString (show ts) <> "-" <> fromString (show ms)
    xRangeStreamIdToStr (ts, Nothing) = fromString (show ts) <> "-"

showStreamId :: ConcreteStreamId -> ByteString
showStreamId (i, s) = fromString (show i) <> "-" <> fromString (show s)

showXReadStreamId :: XReadStreamId -> ByteString
showXReadStreamId Dollar = "$"
showXReadStreamId (Concrete sid) = showStreamId sid
