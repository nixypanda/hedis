{-# LANGUAGE DeriveAnyClass #-}
{-# LANGUAGE DuplicateRecordFields #-}
{-# LANGUAGE OverloadedStrings #-}

module Replication (
    Replication (..),
    ReplicaOf (..),
    ReplicationConfig (..),
    MasterConfig,
    ReplicaConfig (..),
    MasterState (..),
    ReplicaState (..),
    ReplicaConn (..),
    replicationInfo,
    initMasterState,
    initReplicaState,
    acceptReplica,
    cleanupReplicaRegistry,
    propagateWrite,
) where

import Control.Concurrent.Async (Async, async, cancel)
import Control.Concurrent.STM (
    STM,
    TQueue,
    TVar,
    atomically,
    modifyTVar,
    newTQueueIO,
    newTVarIO,
    readTQueue,
    readTVar,
    readTVarIO,
    writeTQueue,
    writeTVar,
 )
import Control.Exception (Exception)
import Data.ByteString (ByteString)
import Data.ByteString qualified as BS
import Data.String (fromString)
import Network.Simple.TCP (Socket, send)

import Command (CmdSTM, Command (..), cmdToResp)
import CommandResult (CommandResult (..), ReplResult (ResFullResync))
import Resp (encode)
import System.Log.FastLogger (LogStr, TimedFastLogger, ToLogStr (toLogStr))

-- CLI flags ──▶ ReplicationConfig
--                 │
--                 ▼
--           Replication (Env)
--            /           \
--   MasterState       ReplicaState
--        │                  │
--     INFO               PSYNC / REPLCONF

-- Used in CLI parsing
data ReplicaOf = MkReplicaOf
    { masterHost :: String
    , masterPort :: Int
    }
    deriving (Show, Eq)

-- ReplicationConfig is stuff read from CLI
data ReplicationConfig = RCMaster MasterConfig | RCReplica ReplicaConfig
    deriving (Show)

type MasterConfig = ()
data ReplicaConfig = MkReplicaConfig {masterInfo :: ReplicaOf, localPort :: Int}
    deriving (Show)

-- Replication is what we store in the Env
data Replication
    = MkReplicationMaster MasterState
    | MkReplicationReplica ReplicaState

data MasterState = MkMasterState
    { masterReplId :: ByteString
    , masterReplOffset :: TVar Int
    , replicaRegistry :: ReplicaRegistry
    , rdbFile :: FilePath
    }

data ReplicaState = MkReplicaState
    { masterInfo :: ReplicaOf
    , knownMasterRepl :: TVar ByteString
    , replicaOffset :: TVar Int
    , localPort :: Int
    }

data ReplicaConn = MkReplicaConn
    { rcSocket :: Socket
    , rcOffset :: TVar Int
    , rcAuxCmdOffset :: TVar Int
    , rcQueue :: TQueue ByteString
    , rcSender :: Async ()
    }

instance Show ReplicaConn where
    show MkReplicaConn{..} = "ReplicaConn " <> show rcSocket

data ReplicationError = ReplicaDisconnected
    deriving (Show, Exception)

type ReplicaRegistry = TVar [ReplicaConn]

initMasterState :: MasterConfig -> IO MasterState
initMasterState _ = do
    replicaRegistry <- newTVarIO []
    masterReplOffset' <- newTVarIO 0
    pure $
        MkMasterState
            { masterReplId = "8371b4fb1155b71f4a04d3e1bc3e18c4a990aeeb"
            , masterReplOffset = masterReplOffset'
            , replicaRegistry = replicaRegistry
            , rdbFile = "./master.rdb"
            }

initReplicaState :: ReplicaConfig -> IO ReplicaState
initReplicaState MkReplicaConfig{..} = do
    replicaOffset <- newTVarIO (-1)
    knownMasterRepl <- newTVarIO "?"
    pure $ MkReplicaState{..}

replicationInfo :: Replication -> IO ByteString
replicationInfo (MkReplicationMaster (MkMasterState{..})) = do
    offset <- readTVarIO masterReplOffset
    pure $
        BS.intercalate
            "\r\n"
            [ "role:master"
            , "master_replid:" <> masterReplId
            , "master_repl_offset:" <> fromString (show offset)
            ]
replicationInfo (MkReplicationReplica (MkReplicaState{})) = pure "role:slave"

propagateWrite :: Replication -> CmdSTM -> STM ()
propagateWrite repli cmd =
    case repli of
        MkReplicationMaster (MkMasterState{..}) -> do
            registry <- readTVar replicaRegistry
            mapM_ (\rc -> writeTQueue rc.rcQueue $ encode $ cmdToResp $ RedSTM cmd) registry
        MkReplicationReplica _ ->
            pure () -- replicas never propagate

acceptReplica :: TimedFastLogger -> MasterState -> Socket -> IO CommandResult
acceptReplica envLogger masterState sock = do
    _ <- initReplica envLogger masterState sock
    masterReplOffset' <- readTVarIO masterState.masterReplOffset
    pure $ RRepl $ ResFullResync masterState.masterReplId masterReplOffset'

initReplica :: TimedFastLogger -> MasterState -> Socket -> IO ()
initReplica envLogger masterState rcSocket = do
    rcOffset <- newTVarIO (-1)
    rcQueue <- newTQueueIO
    rcAuxCmdOffset <- newTVarIO 0

    rcSender <- async $ replicaSender envLogger rcSocket masterState.rdbFile rcQueue
    let rc = MkReplicaConn{..}
    atomically $ modifyTVar masterState.replicaRegistry (rc :)
    pure ()

cleanupReplicaRegistry :: ReplicaRegistry -> IO ()
cleanupReplicaRegistry reg = do
    replicas <- readTVarIO reg
    mapM_ (cancel . (.rcSender)) replicas
    atomically $ writeTVar reg []

replicaSender :: TimedFastLogger -> Socket -> FilePath -> TQueue ByteString -> IO ()
replicaSender envLogger sock rdbFile q = do
    -- Phase 1: RDB snapshot
    rdbFileData <- readRdbFile rdbFile
    let respEncoded = "$" <> fromString (show $ BS.length rdbFileData) <> "\r\n" <> rdbFileData
    send sock respEncoded

    -- Phase 2: Write Command Propogation
    replicaSenderLoop envLogger sock q

replicaSenderLoop :: TimedFastLogger -> Socket -> TQueue ByteString -> IO ()
replicaSenderLoop envLogger sock q = do
    resp <- atomically $ readTQueue q
    send sock resp
    replicaSenderLoop envLogger sock q

readRdbFile :: FilePath -> IO ByteString
readRdbFile = BS.readFile

---------------
loggingFuncs :: (Show a) => ((a -> LogStr) -> t) -> (String -> t, String -> t)
loggingFuncs envLogger =
    let logInfo' msg = envLogger (\t -> toLogStr (show t <> "[REPLICATION][INFO] " <> msg <> "\n"))
        logError' msg = envLogger (\t -> toLogStr (show t <> "[REPLICATION][ERROR] " <> msg <> "\n"))
     in (logInfo', logError')
