module ListMap where

import Data.Map (Map)
import Data.Map qualified as M
import Data.Maybe (fromMaybe)

type ListMap k v = Map k [v]

data Range = MkRange {start :: Int, end :: Int}
    deriving (Show, Eq)

empty :: ListMap k v
empty = M.empty

append :: (Ord k) => k -> [v] -> ListMap k v -> ListMap k v
append k vals lMap = case M.lookup k lMap of
    Nothing -> M.insert k vals lMap
    Just xs -> M.insert k (xs ++ vals) lMap

revPrepend :: (Ord k) => k -> [v] -> ListMap k v -> ListMap k v
revPrepend k vals lMap = case M.lookup k lMap of
    Nothing -> M.insert k vals lMap
    Just xs -> M.insert k (reverse vals ++ xs) lMap

lookup :: (Ord k) => k -> ListMap k v -> Maybe [v]
lookup = M.lookup

lookupCount :: (Ord k) => k -> ListMap k v -> Int
lookupCount k = maybe 0 length . M.lookup k

leftPops :: (Ord k) => k -> Int -> ListMap k v -> ([v], ListMap k v)
leftPops k count lMap = (pop, M.insert k keep lMap)
  where
    (pop, keep) = splitAt count xs
    xs = fromMaybe [] $ M.lookup k lMap

leftPop :: (Ord k) => k -> ListMap k v -> Maybe (v, ListMap k v)
leftPop k lMap = case M.lookup k lMap of
    Nothing -> Nothing
    Just [] -> Nothing
    Just (x : xs) -> Just (x, M.insert k xs lMap)

list :: (Ord k) => k -> Range -> ListMap k v -> [v]
list k MkRange{..} = maybe [] (slice start end) . M.lookup k

normalize :: Int -> Int -> Int
normalize len i
    | i < 0 = max 0 (len + i)
    | otherwise = i

slice :: Int -> Int -> [a] -> [a]
slice start stop xs =
    let len = length xs
        start' = normalize len start
        stop' = normalize len stop
        count = stop' - start' + 1
     in if count <= 0
            then []
            else take count (drop start' xs)
