{-# LANGUAGE GADTs #-}
{-# LANGUAGE OverloadedStrings #-}

module Execution.Base (runCmdSTM, runCmdIO, runServerInfoCmds, runConfigInfoCmds) where

import Control.Concurrent.STM (STM, atomically)
import Control.Monad.IO.Class (MonadIO (liftIO))
import Control.Monad.Reader (asks)
import Data.List (singleton)
import Data.Time (UTCTime)

import Data.String (IsString (..))
import Protocol.Command
import Protocol.Result
import Replication.Config (HasRdbConfig (..), replicationInfo)
import Store.ListStore qualified as LS
import Store.StreamStore qualified as StS
import Store.StringStore qualified as SS
import Store.TypeStore qualified as TS
import Time (timeout')
import Types.Redis

runCmdSTM :: (HasStores r) => Env r -> UTCTime -> CmdSTM -> STM (Either CommandError CommandResult)
runCmdSTM env now cmd = do
    let tvListMap = getListStore env
        tvStringMap = getStringStore env
        tvTypeIndex = getTypeIndex env
        tvStreamMap = getStreamStore env
    case cmd of
        CmdPing -> pure $ Right ResPong
        CmdEcho xs -> pure $ Right $ RBulk (Just xs)
        -- type index
        CmdType x -> do
            ty <- TS.getTypeSTM tvTypeIndex x
            pure $ Right $ ResType ty
        STMString c -> SS.runStringStoreSTM tvTypeIndex tvStringMap now c
        STMList c -> Right <$> LS.runListStoreSTM tvTypeIndex tvListMap c
        STMStream c -> StS.runStreamStoreSTM tvTypeIndex tvStreamMap now c

runCmdIO :: (HasStores r) => CmdIO -> Redis r CommandResult
runCmdIO cmd = do
    tvListMap <- asks getListStore
    tvStreamMap <- asks getStreamStore
    case cmd of
        -- List Store
        CmdBLPop key 0 -> do
            vals <- liftIO $ atomically (LS.blpopSTM tvListMap key)
            pure $ RArraySimple vals
        CmdBLPop key t -> do
            val <- liftIO (timeout' t (atomically (LS.blpopSTM tvListMap key)))
            pure $ maybe RArrayNull RArraySimple val
        -- Stream Store
        CmdXReadBlock key sid 0 -> do
            sid' <- liftIO $ atomically (StS.xResolveStreamIdSTM tvStreamMap key sid)
            vals <- liftIO (atomically (StS.xReadBlockSTM tvStreamMap key sid'))
            pure $ (RArrayKeyValues . singleton) vals
        CmdXReadBlock key sid tout -> do
            sid' <- liftIO $ atomically (StS.xResolveStreamIdSTM tvStreamMap key sid)
            vals <- liftIO (timeout' tout (atomically (StS.xReadBlockSTM tvStreamMap key sid')))
            pure $ maybe RArrayNull (RArrayKeyValues . singleton) vals

runServerInfoCmds :: (HasReplication r) => Env r -> Maybe SubInfo -> STM CommandResult
runServerInfoCmds env cmd = do
    case cmd of
        Just IReplication -> do
            info <- replicationInfo $ getReplication env
            pure $ RBulk $ Just info
        _ -> error "not handled"

runConfigInfoCmds :: Env r -> SubConfig -> CommandResult
runConfigInfoCmds env cmd = case env of
    EnvMaster _ ms -> f ms
    EnvReplica _ rs -> f rs
  where
    f :: (HasRdbConfig r) => r -> CommandResult
    f s = case cmd of
        ConfigDir -> RArraySimple ["dir", fromString $ rdbDir s]
        ConfigDbFilename -> RArraySimple ["dbfilename", fromString $ rdbFilename s]
