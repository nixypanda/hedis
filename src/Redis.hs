{-# LANGUAGE DataKinds #-}
{-# LANGUAGE DeriveAnyClass #-}
{-# LANGUAGE DerivingStrategies #-}
{-# LANGUAGE GADTs #-}
{-# LANGUAGE KindSignatures #-}
{-# LANGUAGE OverloadedStrings #-}

module Redis (
    Env (..),
    Redis (..),
    RedisError (..),
    Master,
    Replica,
    HasLogger (..),
    HasStores (..),
    HasReplication (..),
    ClientState (..),
    mkMasterEnv,
    mkReplicaEnv,
    runReplication,
    TxState (..),
    clientLoopDiscard,
    clientLoopWrite,
) where

import Control.Concurrent.STM (
    STM,
    TVar,
    atomically,
    modifyTVar,
    newTVarIO,
    readTVarIO,
    writeTVar,
 )
import Control.Exception (Exception)
import Control.Monad (forM_, when)
import Control.Monad.Except (ExceptT (..), MonadError (throwError), liftEither)
import Control.Monad.IO.Class (MonadIO (liftIO))
import Control.Monad.Reader (MonadReader (ask), ReaderT, asks)
import Data.Bifunctor (Bifunctor (first))
import Data.ByteString (ByteString)
import Data.Kind (Type)
import Data.List (find, singleton)
import Data.String (fromString)
import Data.Time (NominalDiffTime, UTCTime, getCurrentTime)
import Network.Simple.TCP (Socket, recv, send)
import System.Log.FastLogger (
    LogType' (..),
    TimedFastLogger,
    ToLogStr (toLogStr),
    defaultBufSize,
    newTimeCache,
    newTimedFastLogger,
    simpleTimeFormat,
 )
import System.Timeout (timeout)
import Text.Parsec (ParseError)

import Command
import CommandResult
import Control.Concurrent (threadDelay)
import Parsers (parseWithRemainder)
import RDB (rdbParser)
import Replication (
    MasterConfig,
    MasterState (..),
    ReplicaConfig,
    ReplicaConn (..),
    ReplicaState (..),
    Replication (..),
    acceptReplica,
    initMasterState,
    initReplicaState,
    propagateWrite,
    replicationInfo,
 )
import Resp (Resp (..), decodeMany, encode)
import Store.ListStore (ListStore)
import Store.ListStore qualified as LS
import Store.StreamStore (StreamStore)
import Store.StreamStore qualified as StS
import Store.StringStore (StringStore)
import Store.StringStore qualified as SS
import Store.TypeStore (IncorrectType, TypeIndex)
import Store.TypeStore qualified as TS
import Time (nominalDiffTimeToMicros)

-- Types

data RedisError
    = ParsingError ParseError
    | EncodeError String
    | EmptyBuffer
    | Unimplemented String
    | ConversionError String
    | InvalidCommand String
    | WrongType IncorrectType
    | HandshakeError HandshakeError
    deriving (Show, Exception)

data HandshakeError
    = InvalidReturn Command ByteString ByteString
    deriving (Show, Eq)

-- Transactions

data TxState = NoTx | InTx [CmdSTM] -- queue only STM-capable commands
    deriving (Show)

-- Env

data Stores = MkStores
    { stringStore :: TVar StringStore
    , listStore :: TVar ListStore
    , typeIndex :: TVar TypeIndex
    , streamStore :: TVar StreamStore
    }

data Master
data Replica

data Env (r :: Type) where
    EnvMaster :: CommonEnv -> MasterState -> Env Master
    EnvReplica :: CommonEnv -> ReplicaState -> Env Replica

data CommonEnv = MkCommonEnv
    { stores :: Stores
    , envLogger :: TimedFastLogger
    }

mkCommonEnv :: IO CommonEnv
mkCommonEnv = do
    stores <- atomically $ do
        stringStore <- SS.emptySTM
        listStore <- LS.emptySTM
        typeIndex <- TS.emptySTM
        streamStore <- StS.emptySTM
        pure $ MkStores{..}

    timeCache <- newTimeCache simpleTimeFormat
    (envLogger, _cleanup) <- newTimedFastLogger timeCache (LogStderr defaultBufSize)
    pure $ MkCommonEnv stores envLogger

mkMasterEnv :: MasterConfig -> IO (Env Master)
mkMasterEnv mc = EnvMaster <$> mkCommonEnv <*> initMasterState mc

mkReplicaEnv :: ReplicaConfig -> IO (Env Replica)
mkReplicaEnv rc = EnvReplica <$> mkCommonEnv <*> initReplicaState rc

data ClientState = MkClientState
    { txState :: TVar TxState
    , socket :: Socket
    }

newtype Redis r a = MkRedis {runRedis :: ReaderT (Env r) (ExceptT RedisError IO) a}
    deriving newtype (Functor, Applicative, Monad, MonadError RedisError, MonadIO, MonadReader (Env r))

class HasStores r where
    getStores :: Env r -> Stores

instance HasStores Master where
    getStores :: Env Master -> Stores
    getStores (EnvMaster c _) = c.stores

instance HasStores Replica where
    getStores :: Env Replica -> Stores
    getStores (EnvReplica c _) = c.stores

class HasLogger r where
    getLogger :: Env r -> TimedFastLogger

instance HasLogger Master where
    getLogger :: Env Master -> TimedFastLogger
    getLogger (EnvMaster c _) = c.envLogger

instance HasLogger Replica where
    getLogger :: Env Replica -> TimedFastLogger
    getLogger (EnvReplica c _) = c.envLogger

class HasReplication r where
    getReplication :: Env r -> Replication
    getOffset :: Env r -> TVar Int

instance HasReplication Master where
    getReplication :: Env Master -> Replication
    getReplication (EnvMaster _ ms) = MkReplicationMaster ms

    getOffset (EnvMaster _ ms) = ms.masterReplOffset

instance HasReplication Replica where
    getReplication :: Env Replica -> Replication
    getReplication (EnvReplica _ rs) = MkReplicationReplica rs

    getOffset (EnvReplica _ rs) = rs.replicaOffset

-- Execution

runCmd :: (HasStores r, HasReplication r, HasLogger r) => ClientState -> Command -> Redis r (Maybe CommandResult)
runCmd clientState cmd = do
    env <- ask
    now <- liftIO getCurrentTime
    txState <- liftIO $ readTVarIO clientState.txState

    case txState of
        InTx cs ->
            Just <$> case cmd of
                RedSTM c -> liftIO $ atomically $ do
                    writeTVar clientState.txState (InTx $ c : cs)
                    pure $ RSimple "QUEUED"
                RedTrans m -> case m of
                    Multi -> pure $ RErr $ RTxErr RMultiInMulti
                    Exec -> liftIO $ atomically $ executeQueuedCmds clientState env now cs
                    Discard -> liftIO $ atomically $ do
                        writeTVar clientState.txState NoTx
                        pure $ RSimple "OK"
                _ -> pure $ RErr $ RTxErr RNotSupportedInTx
        NoTx -> case cmd of
            RedSTM cmd' -> Just <$> liftIO (atomically $ runAndReplicate env now cmd')
            RedIO cmd' -> Just <$> runCmdIO cmd'
            RedTrans txCmd ->
                Just <$> case txCmd of
                    Exec -> pure $ RErr $ RTxErr RExecWithoutMulti
                    Discard -> pure $ RErr $ RTxErr RDiscardWithoutMulti
                    Multi -> liftIO $ atomically $ do
                        modifyTVar clientState.txState (const $ InTx [])
                        pure $ RSimple "OK"
            RedRepl c -> runReplicationCmds clientState c
            RedInfo section -> Just <$> runServerInfoCmds section

runServerInfoCmds :: (MonadReader (Env r) m, HasReplication r, MonadIO m) => Maybe SubInfo -> m CommandResult
runServerInfoCmds cmd = do
    env <- ask
    case cmd of
        Just IReplication -> do
            info <- liftIO $ replicationInfo $ getReplication env
            pure $ RBulk $ Just info
        _ -> error "not handled"

runReplicationCmds :: (HasReplication r, HasLogger r) => ClientState -> CmdReplication -> Redis r (Maybe CommandResult)
runReplicationCmds clientState cmd = do
    env <- ask
    case cmd of
        CmdReplConfCapabilities -> pure $ Just $ RSimple "OK"
        CmdReplConfListen _ -> pure $ Just $ RSimple "OK"
        CmdPSync "?" (-1) -> case getReplication env of
            MkReplicationMaster masterState -> do
                let rcSocket = clientState.socket
                Just <$> liftIO (acceptReplica (getLogger env) masterState rcSocket)
            MkReplicationReplica _ -> error "called on replica" -- handle later
        CmdPSync _ _ -> error "not handled"
        CmdFullResync _ _ -> error "not handled"
        CmdReplConfGetAck -> do
            offset <- liftIO $ readTVarIO (getOffset env)
            pure $ Just $ RRepl $ ReplConfAck offset
        CmdWait n tout -> Just <$> sendReplConfs clientState n tout
        CmdReplConfAck offset -> do
            logInfo $ "ACK offset=" <> show offset
            logInfo $ "received on socket " <> show clientState.socket

            _ <- case getReplication env of
                MkReplicationMaster masterState -> do
                    registry <- liftIO $ readTVarIO masterState.replicaRegistry
                    let findReplica = find (\r -> r.rcSocket == clientState.socket) registry
                    case findReplica of
                        Nothing -> error "f this shit how to figure it out"
                        Just r -> do
                            liftIO $ atomically $ writeTVar r.rcOffset offset
                            pure ()
                MkReplicationReplica _ -> error "called on replica"

            pure Nothing

sendReplConfs :: (HasReplication r) => ClientState -> Int -> NominalDiffTime -> Redis r CommandResult
sendReplConfs _clientState n tout = do
    env <- ask
    case getReplication env of
        MkReplicationMaster masterState -> do
            registry <- liftIO $ readTVarIO masterState.replicaRegistry
            targetOffset <- liftIO $ readTVarIO masterState.masterReplOffset

            liftIO $ print $ "WAIT targetOffset=" <> show targetOffset <> " replicas=" <> show n <> " timeout=" <> show tout

            -- trigger GETACK
            liftIO $ forM_ registry $ \r ->
                send r.rcSocket (encode $ cmdToResp $ RedRepl CmdReplConfGetAck)

            -- The main server is already listening for commands so we just poll
            let countAcked :: IO Int
                countAcked = do
                    offsets <- mapM (readTVarIO . rcOffset) registry
                    pure $ length (filter (>= targetOffset) offsets)

                poll :: IO Int
                poll = do
                    acked <- countAcked
                    if acked >= n
                        then pure acked
                        else do
                            threadDelay 1000
                            poll

            mres <- liftIO $ timeout' tout poll
            acked <- liftIO $ maybe countAcked pure mres
            pure $ RInt $ if targetOffset > 0 then acked else length registry
        MkReplicationReplica _ ->
            error "WAIT called on replica"

executeQueuedCmds :: (HasStores r, HasReplication r) => ClientState -> Env r -> UTCTime -> [CmdSTM] -> STM CommandResult
executeQueuedCmds clientState env now cmds = do
    vals <- mapM (runAndReplicate env now) (reverse cmds)
    modifyTVar clientState.txState (const NoTx)
    pure $ RArray vals

runAndReplicate :: (HasReplication r, HasStores r) => Env r -> UTCTime -> CmdSTM -> STM CommandResult
runAndReplicate env now cmd = do
    res <- runCmdSTM env now cmd
    when (isWriteCmd cmd) $ do
        propagateWrite (getReplication env) cmd
        modifyTVar (getOffset env) (+ cmdSTMBytes cmd)
    pure res

runCmdSTM :: (HasStores r) => Env r -> UTCTime -> CmdSTM -> STM CommandResult
runCmdSTM env now cmd = do
    let tvListMap = (listStore . getStores) env
        tvStringMap = (stringStore . getStores) env
        tvTypeIndex = (typeIndex . getStores) env
        tvStreamMap = (streamStore . getStores) env
    case cmd of
        CmdPing -> pure ResPong
        CmdEcho xs -> pure $ RBulk (Just xs)
        -- type index
        CmdType x -> do
            ty <- TS.getTypeSTM tvTypeIndex x
            pure $ ResType ty
        STMString c -> SS.runStringStoreSTM tvTypeIndex tvStringMap now c
        STMList c -> LS.runListStoreSTM tvTypeIndex tvListMap c
        STMStream c -> StS.runStreamStoreSTM tvTypeIndex tvStreamMap now c

runCmdIO :: (MonadReader (Env r) m, HasStores r, MonadIO m) => CmdIO -> m CommandResult
runCmdIO cmd = do
    tvListMap <- asks (listStore . getStores)
    tvStreamMap <- asks (streamStore . getStores)
    case cmd of
        -- List Store
        CmdBLPop key 0 -> do
            vals <- liftIO $ atomically (LS.blpopSTM tvListMap key)
            pure $ RArraySimple vals
        CmdBLPop key t -> do
            val <- liftIO (timeout' t (atomically (LS.blpopSTM tvListMap key)))
            pure $ maybe RArrayNull RArraySimple val
        -- Stream Store
        CmdXReadBlock key sid 0 -> do
            sid' <- liftIO $ atomically (StS.xResolveStreamIdSTM tvStreamMap key sid)
            vals <- liftIO (atomically (StS.xReadBlockSTM tvStreamMap key sid'))
            pure $ (RArrayKeyValues . singleton) vals
        CmdXReadBlock key sid tout -> do
            sid' <- liftIO $ atomically (StS.xResolveStreamIdSTM tvStreamMap key sid)
            vals <- liftIO (timeout' tout (atomically (StS.xReadBlockSTM tvStreamMap key sid')))
            pure $ maybe RArrayNull (RArrayKeyValues . singleton) vals

runReplication :: Socket -> Redis Replica ()
runReplication socket = do
    env <- ask
    case env of
        EnvReplica _ (MkReplicaState{..}) -> do
            -- Ping
            let cmd1 = RedSTM CmdPing
            liftIO $ send socket $ encode $ cmdToResp cmd1
            (r1, buf1) <- liftIO $ recvResp1 socket mempty
            case r1 of
                Str s | s == "PONG" -> pure ()
                other -> throwError $ HandshakeError $ InvalidReturn cmd1 "PONG" (fromString $ show other)

            -- Replconf
            let cmd2 = RedRepl (CmdReplConfListen localPort)
            liftIO $ send socket $ encode $ cmdToResp cmd2
            (r2, buf2) <- liftIO $ recvResp1 socket buf1
            case r2 of
                Str s | s == "OK" -> pure ()
                other -> throwError $ HandshakeError $ InvalidReturn cmd2 "OK" (fromString $ show other)

            -- Replconf capabilities
            let cmd3 = RedRepl CmdReplConfCapabilities
            liftIO $ send socket $ encode $ cmdToResp cmd3
            (r3, buf3) <- liftIO $ recvResp1 socket buf2
            case r3 of
                Str s | s == "OK" -> pure ()
                other -> throwError $ HandshakeError $ InvalidReturn cmd3 "OK" (fromString $ show other)

            -- Psync
            knownMasterRepl' <- liftIO $ readTVarIO knownMasterRepl
            replicaOffset' <- liftIO $ readTVarIO replicaOffset
            let cmd4 = RedRepl $ CmdPSync knownMasterRepl' replicaOffset'
            liftIO $ send socket $ encode $ cmdToResp cmd4
            (r4, buf4) <- liftIO $ recvResp1 socket buf3
            case respToCmd r4 of
                Right (RedRepl (CmdFullResync sid _)) -> liftIO . atomically $ do
                    writeTVar knownMasterRepl sid
                    writeTVar replicaOffset 0
                _ -> throwError $ HandshakeError $ InvalidReturn cmd4 "FULLRESYNC <repli_id> 0" (fromString $ show r4)

            logInfo $ "Handshake complete. Waiting for RDB" <> show socket
            txState <- liftIO $ newTVarIO NoTx

            let recvMore buf = do
                    bs <- liftIO (recv socket 4096) >>= maybe (throwError EmptyBuffer) pure
                    pure (buf <> bs)

                go buf = do
                    case parseWithRemainder rdbParser buf of
                        Right (rdb, rest) -> do
                            logInfo $ "Received RDB : " <> show rdb
                            clientLoopDiscard MkClientState{..} socket rest
                        Left _ -> recvMore buf >>= go

            go buf4

recvResp1 :: Socket -> ByteString -> IO (Resp, ByteString)
recvResp1 sock = go
  where
    go acc = do
        case decodeMany acc of
            Right (r : _, rest) -> pure (r, rest)
            Right ([], _) -> recvMore acc
            Left _ -> recvMore acc

    recvMore acc = do
        bs <- recv sock 64 >>= maybe (fail "EOF") pure
        go (acc <> bs)

-- Command execution

processOne :: (HasLogger r, HasStores r, HasReplication r) => ClientState -> Resp -> Redis r (Maybe CommandResult)
processOne clientState resp = do
    cmd <- liftEither $ first ConversionError $ respToCmd resp
    runCmd clientState cmd

processOneWrite :: (HasLogger r, HasStores r, HasReplication r) => ClientState -> Socket -> Resp -> Redis r ()
processOneWrite clientState socket resp = do
    result <- processOne clientState resp
    case result of
        Just result' -> do
            let encoded = encode (resultToResp result')
            liftIO $ send socket encoded
            pure ()
        Nothing -> pure ()

clientLoopWrite :: (HasLogger r, HasStores r, HasReplication r) => ClientState -> Socket -> Redis r ()
clientLoopWrite clientState socket = go mempty
  where
    bufferSize = 1024

    go buf = do
        mbs <- liftIO $ recv socket bufferSize
        bs <- maybe (throwError EmptyBuffer) pure mbs

        let buf' = buf <> bs
        case decodeMany buf' of
            Left err -> throwError (ParsingError err)
            Right (resps, rest) -> do
                mapM_ (processOneWrite clientState socket) resps
                go rest

clientLoopDiscard :: ClientState -> Socket -> ByteString -> Redis Replica ()
clientLoopDiscard clientState socket buf = do
    env <- ask
    let bufferSize = 1024
    logInfo $ "Waiting for write command. Current buffer - " <> show buf

    case decodeMany buf of
        Left err -> do
            throwError (ParsingError err)
        Right (resps, rest) -> do
            forM_ resps $ \resp -> do
                res <- processOne clientState resp
                case res of
                    Just r@(RRepl cr) -> case cr of
                        ReplOk -> pure ()
                        ReplConfAck _ -> do
                            liftIO $ atomically $ modifyTVar (getOffset env) (+ respBytes resp)
                            let encoded = encode (resultToResp r)
                            liftIO $ send socket encoded
                        ResFullResync _ _ -> pure ()
                    Just ResPong -> do
                        liftIO $ atomically $ modifyTVar (getOffset env) (+ respBytes resp)
                    _ -> pure ()

            mbs <- liftIO $ recv socket bufferSize
            bs <- maybe (throwError EmptyBuffer) pure mbs
            clientLoopDiscard clientState socket (rest <> bs)

-- helpers

timeout' :: NominalDiffTime -> IO a -> IO (Maybe a)
timeout' t = timeout (nominalDiffTimeToMicros t)

logInfo :: (MonadReader (Env r) m, MonadIO m, HasLogger r, HasReplication r) => String -> m ()
logInfo msg = do
    logger <- asks getLogger
    repInfo <- asks getReplication
    let prefix = case repInfo of
            MkReplicationMaster _ -> "[ MASTER ]"
            MkReplicationReplica _ -> "[ REPLICA ]"

    liftIO $ logger (\t -> toLogStr (show t <> prefix <> "[INFO] " <> msg <> "\n"))
