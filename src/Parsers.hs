{-# LANGUAGE OverloadedStrings #-}

module Parsers (
    bytes,
    signedIntParser,
    signedFloatParser,
    intParser,
    readIntBS,
    readFloatBS,
    readXAddStreamId,
    readXRange,
    readXReadStreamId,
    readConcreteStreamId,
) where

import Control.Applicative ((<|>))
import Control.Monad (join)
import Data.ByteString (ByteString)
import Data.Functor (($>))
import Data.String (fromString)
import StoreBackend.StreamMap (
    ConcreteStreamId,
    IdSeq (..),
    XAddStreamId (..),
    XRange (..),
    XRangeEnd (..),
    XRangeStart (..),
    XRangeStreamId,
    XReadStreamId (..),
 )
import Text.Parsec (
    anyChar,
    char,
    digit,
    eof,
    many1,
    manyTill,
    option,
    optionMaybe,
    parse,
    spaces,
 )
import Text.Parsec.ByteString (Parser)

bytes :: Parser ByteString
bytes = fromString <$> manyTill anyChar (char '\r')

intParser :: Parser Int
intParser = fromIntegral <$> integer

signedIntParser :: Parser Int
signedIntParser = do
    sign <- option '+' (char '+' <|> char '-')
    digits <- many1 digit
    let intValue = read digits :: Int
    return $ if sign == '-' then -intValue else intValue

signedFloatParser :: Parser Float
signedFloatParser = do
    sign <- optionMaybe (char '-')
    i <- many1 digit
    f <- option "" (char '.' *> many1 digit)
    let s = maybe "" pure sign <> i <> if null f then "" else '.' : f
    pure (read s)

parseBS :: Parser a -> ByteString -> Either String a
parseBS p bs =
    case parse (p <* eof) "<resp-arg>" bs of
        Left err -> Left (show err)
        Right x -> Right x

readIntBS :: ByteString -> Either String Int
readIntBS = parseBS signedIntParser

readFloatBS :: ByteString -> Either String Float
readFloatBS = parseBS signedFloatParser

-- Redis stream id

xAddStreamIdParser :: Parser XAddStreamId
xAddStreamIdParser = autoId <|> explicitId

autoId :: Parser XAddStreamId
autoId = char '*' $> AutoId

explicitId :: Parser XAddStreamId
explicitId = do
    ts <- intParser
    _ <- char '-'
    ExplicitId ts <$> seqParser

seqParser :: Parser IdSeq
seqParser = (char '*' $> SeqAuto) <|> (Seq <$> intParser)

integer :: Parser Integer
integer = read <$> many1 digit

readXAddStreamId :: ByteString -> Either String XAddStreamId
readXAddStreamId = parseBS xAddStreamIdParser

-- Xrange

xRangeStreamId :: Parser XRangeStreamId
xRangeStreamId = do
    ts <- intParser
    mSeq <- optionMaybe (char '-' *> optionMaybe intParser)
    pure (ts, join mSeq)

xRangeStartParser :: Parser XRangeStart
xRangeStartParser = (char '-' $> XMinus) <|> (XS <$> xRangeStreamId)

xRangeEndParser :: Parser XRangeEnd
xRangeEndParser = (char '+' $> XPlus) <|> (XE <$> xRangeStreamId)

xRangeParser :: Parser XRange
xRangeParser = do
    s <- xRangeStartParser
    spaces
    e <- xRangeEndParser
    eof
    pure (MkXrange s e)

readXRange :: ByteString -> ByteString -> Either String XRange
readXRange s e = parseBS xRangeParser (s <> " " <> e)

-- Concreate StreamId

concreteStreamIdParser :: Parser ConcreteStreamId
concreteStreamIdParser = do
    ts <- intParser
    _ <- char '-'
    (,) ts <$> intParser

xReadStreamIdParser :: Parser XReadStreamId
xReadStreamIdParser = char '$' $> Dollar <|> (Concrete <$> concreteStreamIdParser)

readConcreteStreamId :: ByteString -> Either String ConcreteStreamId
readConcreteStreamId = parseBS concreteStreamIdParser

readXReadStreamId :: ByteString -> Either String XReadStreamId
readXReadStreamId = parseBS xReadStreamIdParser
