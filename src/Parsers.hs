{-# LANGUAGE OverloadedStrings #-}

module Parsers (
    bytes,
    signedIntParser,
    signedFloatParser,
    intParser,
    readIntBS,
    readFloatBS,
    parseWithRemainder,
    parseBS,
) where

import Control.Applicative ((<|>))
import Data.ByteString (ByteString)
import Data.String (fromString)
import Text.Parsec (
    anyChar,
    char,
    digit,
    eof,
    getInput,
    lookAhead,
    many1,
    manyTill,
    option,
    optionMaybe,
    parse,
    runParser,
 )
import Text.Parsec.ByteString (Parser)
import Text.Parsec.Error (ParseError)

bytes :: Parser ByteString
bytes = fromString <$> manyTill anyChar (lookAhead (char '\r'))

integer :: Parser Integer
integer = read <$> many1 digit

intParser :: Parser Int
intParser = fromIntegral <$> integer

signedIntParser :: Parser Int
signedIntParser = do
    sign <- option '+' (char '+' <|> char '-')
    digits <- many1 digit
    let intValue = read digits :: Int
    return $ if sign == '-' then -intValue else intValue

signedFloatParser :: Parser Float
signedFloatParser = do
    sign <- optionMaybe (char '-')
    i <- many1 digit
    f <- option "" (char '.' *> many1 digit)
    let s = maybe "" pure sign <> i <> if null f then "" else '.' : f
    pure (read s)

parseBS :: Parser a -> ByteString -> Either String a
parseBS p bs =
    case parse (p <* eof) "<resp-arg>" bs of
        Left err -> Left (show err)
        Right x -> Right x

readIntBS :: ByteString -> Either String Int
readIntBS = parseBS signedIntParser

readFloatBS :: ByteString -> Either String Float
readFloatBS = parseBS signedFloatParser

parseWithRemainder :: Parser a -> ByteString -> Either ParseError (a, ByteString)
parseWithRemainder p bs =
    case runParser ((,) <$> p <*> getInput) () "" bs of
        Left e -> Left e
        Right r -> Right r
