module StoreBackend.ExpiringMap (
    ExpiringMap,
    empty,
    insert,
    lookup,
) where

import Data.Map.Strict (Map)
import Data.Map.Strict qualified as M
import Data.Time (NominalDiffTime, UTCTime (..), diffUTCTime)
import Prelude hiding (lookup)

data StoredVal a = MkStoredVal
    { value :: !a
    , storedWhen :: !UTCTime
    , expiration :: !(Maybe NominalDiffTime)
    }
    deriving (Show)

type ExpiringMap k v = Map k (StoredVal v)

empty :: ExpiringMap k v
empty = M.empty

insert :: (Ord k) => k -> v -> UTCTime -> Maybe NominalDiffTime -> ExpiringMap k v -> ExpiringMap k v
insert k value storedWhen expiration db = do
    M.insert k MkStoredVal{..} db

lookup :: (Ord k) => k -> UTCTime -> ExpiringMap k v -> Maybe v
lookup k t db = M.lookup k db >>= getIfNotExpired t

getIfNotExpired :: UTCTime -> StoredVal v -> Maybe v
getIfNotExpired currentTime MkStoredVal{..} = case expiration of
    Nothing -> Just value
    Just expiry ->
        if currentTime `diffUTCTime` storedWhen < expiry
            then Just value
            else Nothing
