{-# LANGUAGE OverloadedStrings #-}

{- HLINT ignore "Use <$>" -}

module Rdb.Parser (parseRdb, headerParser, auxField, metadataParser) where

import Control.Applicative (many)
import Data.Attoparsec.ByteString (Parser, anyWord8, parseOnly, peekWord8, string, take, takeByteString)
import Data.Attoparsec.Combinator (lookAhead)
import Data.Bits (Bits (..), shiftR)
import Data.ByteString (ByteString)
import Data.Functor (($>))
import Data.Time (UTCTime)
import Data.Time.Clock.POSIX (posixSecondsToUTCTime)
import Data.Word (Word64, Word8)

import Protocol.Command (Key)
import Rdb.Binary
import Rdb.Type

import Prelude hiding (take)

parseRdb :: ByteString -> Either String RdbData
parseRdb = parseOnly rdbParser

rdbParser :: Parser RdbData
rdbParser = do
    header <- headerParser
    metadata <- metadataParser
    dbs <- dbParser
    checksum <- crc64Parser
    pure $ MkRdbData{..}

-- header

headerParser :: Parser RdbHeader
headerParser = do
    magic <- string "REDIS"
    version <- take 4
    pure $ MkRdbHeader{..}

-- metadata

metadataParser :: Parser [RdbMetadata]
metadataParser = manyUntilNothing metadataEntry

metadataEntry :: Parser (Maybe RdbMetadata)
metadataEntry = do
    opcode <- peekRdbOpcode

    case opcode of
        OpAux -> opcode8 OpAux *> (Just <$> auxField)
        OpResizeDB -> fail "unsupported metadata entry type"
        -- Metadata ends here
        OpSelectDB -> pure Nothing -- database
        OpEOF -> pure Nothing -- empty file
        _ -> fail ("unexpected opcode in metadata: " <> show opcode)

auxField :: Parser RdbMetadata
auxField = do
    k_len <- rdbLenOnly
    key <- take k_len
    val <- rdbString
    pure (MkMetaAux key val)

-- database

dbParser :: Parser [RdbDatabase]
dbParser = manyUntilNothing dbEntry

dbEntry :: Parser (Maybe RdbDatabase)
dbEntry = do
    opcode <- peekRdbOpcode
    case opcode of
        OpSelectDB -> do
            opcode8 OpSelectDB
            identifier <- rdbLenOnly
            store <- storeParser
            pure . Just $ MkRdbDatabase identifier store
        OpEOF -> pure Nothing
        _ -> fail $ "unsupported db entry type: " <> show opcode

storeParser :: Parser RdbStore
storeParser = do
    opcode <- peekRdbOpcode
    case opcode of
        OpResizeDB -> do
            opcode8 OpResizeDB
            size <- rdbLenOnly
            expirySize <- rdbLenOnly
            table <- many kvEntry
            pure $ MkHashStore $ MkHashTable{..}
        _ -> fail $ "unsupported db entry type: " <> show opcode

kvEntry :: Parser (Key, ByteString, Maybe UTCTime)
kvEntry = do
    opcode <- peekRdbOpcode
    case opcode of
        OpTypeString -> do
            opcode8 OpTypeString
            (k, v) <- kvOnly
            pure (k, v, Nothing)
        OpExpireTime -> do
            opcode8 OpExpireTime
            et <- getInt32Le
            opcode8 OpTypeString
            (k, v) <- kvOnly
            pure (k, v, Just $ posixSecondsToUTCTime $ fromIntegral et)
        OpExpireTimeMs -> do
            opcode8 OpExpireTimeMs
            et <- getInt64Le
            opcode8 OpTypeString
            (k, v) <- kvOnly
            pure (k, v, Just $ posixSecondsToUTCTime (fromIntegral et / 1000))
        _ -> fail $ "unsupported kv entry type: " <> show opcode

kvOnly :: Parser (ByteString, ByteString)
kvOnly = do
    k_len <- rdbLenOnly
    k <- take k_len
    v_len <- rdbLenOnly
    v <- take v_len
    pure (k, v)

-- footer (crc64)

crc64Parser :: Parser Word64
crc64Parser = do
    opcode <- peekRdbOpcode
    case opcode of
        OpEOF -> do
            opcode8 OpEOF
            anyWord64Be
        o -> fail $ "unexpected opcode in footer: " <> show o

-- non-section parsers

opcode8 :: RdbOpcode -> Parser ()
opcode8 expected = do
    actual <- peekRdbOpcode
    if actual == expected
        then anyWord8 $> ()
        else fail $ "expected " <> show expected <> ", got " <> show actual

peekOpcode :: Parser Word8
peekOpcode =
    peekWord8 >>= maybe (fail "unexpected EOF") pure

peekRdbOpcode :: Parser RdbOpcode
peekRdbOpcode = do
    b <- peekOpcode
    pure $ case b of
        0xFA -> OpAux
        0xFB -> OpResizeDB
        0xFE -> OpSelectDB
        0xFF -> OpEOF
        0x00 -> OpTypeString
        0xFD -> OpExpireTime
        0xFC -> OpExpireTimeMs
        x -> OpUnknown x

rdbLenOnly :: Parser Int
rdbLenOnly = do
    b <- anyWord8
    case b `shiftR` 6 of
        0b00 -> pure $ fromIntegral (b .&. 0x3F)
        0b01 -> do
            b2 <- anyWord8
            pure $ (fromIntegral (b .&. 0x3F) `shiftL` 8) .|. fromIntegral b2
        0b10 -> getInt32Le
        _ -> fail $ "special encoding not supported: " <> show b

rdbLen :: Parser (Either MetaVal Int)
rdbLen = do
    b <- anyWord8
    case b `shiftR` 6 of
        0b00 -> pure . Right $ fromIntegral (b .&. 0x3F)
        0b01 -> do
            b2 <- anyWord8
            pure . Right $ (fromIntegral (b .&. 0x3F) `shiftL` 8) .|. fromIntegral b2
        0b10 -> Right <$> getInt32Le
        0b11 -> case b .&. 0x3F of
            0 -> Left . MVInt <$> getInt8Be
            1 -> Left . MVInt <$> getInt16Le
            2 -> Left . MVInt <$> getInt32Le
            r -> do
                rest <- lookAhead takeByteString
                fail $ "special encoding not supported: - " <> show r <> "remainging: " <> show (hexDump rest)
        _ -> fail "impossible"

rdbString :: Parser MetaVal
rdbString = do
    len <- rdbLen
    case len of
        Right len' -> MVString <$> take len'
        Left mval -> pure mval

manyUntilNothing :: Parser (Maybe a) -> Parser [a]
manyUntilNothing p = go []
  where
    go acc = do
        r <- p
        case r of
            Nothing -> pure (reverse acc)
            Just x -> go (x : acc)
