module Rdb.Load where

import Control.Concurrent.STM
import Data.ByteString (ByteString)
import Data.Foldable (foldl')
import Data.Map qualified as M
import Data.Time (UTCTime)

import Protocol.Command
import Rdb.Parser (parseRdb)
import Rdb.Type
import Store.ListStore (ListStore)
import Store.StreamStore (StreamStore)
import Store.StringStore (StringStore)
import Store.TypeStore (TypeIndex)
import StoreBackend.ExpiringMap
import StoreBackend.TypeIndex (ValueType (VString))
import Types.Redis

data StoreState = InitialState
    { ssstringStore :: StringStore
    , sslistStore :: ListStore
    , ssstreamStore :: StreamStore
    , sstypeIndex :: TypeIndex
    }

emptyState :: StoreState
emptyState =
    InitialState
        { ssstringStore = M.empty
        , sslistStore = M.empty
        , ssstreamStore = M.empty
        , sstypeIndex = M.empty
        }

loadRdbIntoLiveState :: Stores -> StoreState -> STM ()
loadRdbIntoLiveState MkStores{..} InitialState{..} = do
    writeTVar stringStore ssstringStore
    writeTVar listStore sslistStore
    writeTVar streamStore ssstreamStore
    writeTVar typeIndex sstypeIndex

materializeRdb :: UTCTime -> RdbData -> StoreState
materializeRdb now rdb =
    foldl' (applyDb now) emptyState (dbs rdb)

applyDb :: UTCTime -> StoreState -> RdbDatabase -> StoreState
applyDb t st (MkRdbDatabase _ (MkHashStore ht)) =
    foldl' (insertKV t) st (table ht)

insertKV :: UTCTime -> StoreState -> (Key, ByteString) -> StoreState
insertKV defaultTime st (k, v) =
    st
        { ssstringStore =
            M.insert k (MkStoredVal v defaultTime Nothing) (ssstringStore st)
        , sstypeIndex =
            M.insert k VString (sstypeIndex st)
        }

---

loadRdbData :: UTCTime -> ByteString -> Stores -> STM (Either String ())
loadRdbData t rdbBinary stores = do
    let rdb = parseRdb rdbBinary
    case rdb of
        Left err -> pure $ Left err
        Right rdb' -> do
            let ss = materializeRdb t rdb'
            Right <$> loadRdbIntoLiveState stores ss
