{-# LANGUAGE ApplicativeDo #-}
{-# LANGUAGE OverloadedStrings #-}

module Cli (CliOptions (..), cliArgsParser) where

import Options.Applicative (
    Parser,
    ParserInfo,
    auto,
    eitherReader,
    fullDesc,
    help,
    helper,
    info,
    long,
    metavar,
    option,
    optional,
    progDesc,
    showDefault,
    strOption,
    value,
    (<**>),
 )

import Types.Replication (RdbConfig (..), ReplicaOf (MkReplicaOf))

data CliOptions = MkCliOptions
    { port :: Int
    , replicaOf :: Maybe ReplicaOf
    , rdbConfig :: RdbConfig
    }

cliArgsParser :: ParserInfo CliOptions
cliArgsParser = info (optsParser <**> helper) (fullDesc <> progDesc "Hedis (A Redis toy-clone)")

optsParser :: Parser CliOptions
optsParser = MkCliOptions <$> portParser <*> replicaOfParser <*> rdbConfigParser

rdbConfigParser :: Parser RdbConfig
rdbConfigParser = MkRdbConfig <$> dirParser <*> dbFilenameParser

dirParser :: Parser FilePath
dirParser =
    strOption
        ( long "dir"
            <> metavar "DIR"
            <> value "."
            <> showDefault
            <> help "Directory for persistence files"
        )

dbFilenameParser :: Parser FilePath
dbFilenameParser =
    strOption
        ( long "dbfilename"
            <> metavar "FILENAME"
            <> value "master.rdb"
            <> showDefault
            <> help "RDB filename"
        )

portParser :: Parser Int
portParser =
    option
        auto
        ( long "port"
            <> metavar "PORT"
            <> value 6379
            <> showDefault
            <> help "Port to listen on"
        )

replicaOfParser :: Parser (Maybe ReplicaOf)
replicaOfParser =
    optional $
        option
            (eitherReader parseReplicaOf)
            ( long "replicaof"
                <> metavar "\"HOST PORT\""
                <> help "Make this server a replica of the given master"
            )

parseReplicaOf :: String -> Either String ReplicaOf
parseReplicaOf s =
    case words s of
        [host, portStr] ->
            case reads portStr of
                [(port, "")] -> Right (MkReplicaOf host port)
                _ -> Left "Invalid master port"
        _ -> Left "Expected \"<host> <port>\""
